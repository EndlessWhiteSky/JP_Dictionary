
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает настройки подсказок канджи в регистр сведений.
// 
// Параметры:
//  СтруктураНастроек - Структура:
//   * КоличествоРядовПодсказок - Число - Количество рядов подсказок.
//   * КоличествоСтолбцовПодсказок - Число - Количество столбцов подсказок.
Процедура ЗаписатьНастройки(Знач СтруктураНастроек) Экспорт
	
	Попытка
		Запись = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, СтруктураНастроек);
		Запись.Записать();
	Исключение
		ВызватьИсключение НСтр("ru = 'Не удалось записать настройки подсказок канджи'") + Символы.ПС + ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

// Возвращает текущие настройки подсказок канджи, объединяя значения по умолчанию с сохраненными в регистре сведений.
// 
// Возвращаемое значение:
//  Структура - Текущие настройки:
// * КоличествоРядовПодсказок - Число - КоличествоРядовПодсказок
// * КоличествоСтолбцовПодсказок - Число - КоличествоСтолбцовПодсказок
Функция ТекущиеНастройки() Экспорт
	Результат = НастройкиПоУмолчанию();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПодсказокКанджи.КоличествоРядовПодсказок КАК КоличествоРядовПодсказок,
		|	НастройкиПодсказокКанджи.КоличествоСтолбцовПодсказок КАК КоличествоСтолбцовПодсказок
		|ИЗ
		|	РегистрСведений.НастройкиПодсказокКанджи КАК НастройкиПодсказокКанджи";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиПоУмолчанию()
	Результат = Новый Структура("КоличествоРядовПодсказок, КоличествоСтолбцовПодсказок", 2, 6);
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли

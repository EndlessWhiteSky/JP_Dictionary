#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДублиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДубли(Команда)
	ОбновитьДублиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДубли(Команда)
	
	МассивСтрок = Элементы.Дубли.ВыделенныеСтроки;
	МассивСсылок = Новый Массив;
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		
		СсылкаНаУдаление = Дубли.НайтиПоИдентификатору(СтрокаМассива).Ссылка;    
		МассивСсылок.Добавить(СсылкаНаУдаление);
		
	КонецЦикла;
	УдалитьДублиНаСервере(МассивСсылок);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДублиНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Словарь.Наименование КАК Наименование,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ втДубли
	|ИЗ
	|	Справочник.Словарь КАК Словарь
	|
	|СГРУППИРОВАТЬ ПО
	|	Словарь.Наименование
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Словарь.Ссылка КАК Ссылка,
	|	Словарь.Наименование КАК Наименование,
	|	Словарь.ЗаписьНаЯпонском КАК ЗаписьНаЯпонском,
	|	Словарь.Комментарий КАК Значение
	|ИЗ
	|	Справочник.Словарь КАК Словарь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДубли КАК втДубли
	|		ПО Словарь.Наименование = втДубли.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	Дубли.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДублиНаСервере(МассивСсылок)
	
	Для Каждого Слово Из МассивСсылок Цикл
		
		СловоОбъект = Слово.ПолучитьОбъект();
		СловоОбъект.Удалить();
		
	КонецЦикла;
	
	ОбновитьДублиНаСервере();
	
КонецПроцедуры

#КонецОбласти
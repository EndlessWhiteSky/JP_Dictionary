
&НаСервере
Процедура ЗаполнитьСлогиНаСервере()
	
	СоответствиеРК = АзбукаВызовСервера.СоответствиеРомаджиКириллицы();
	СоответствиеРДС = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	Слоги = Слоги();
	Отбор = Новый Структура("Наименование");
	НачатьТранзакцию();
	Для Каждого Элемент Из СоответствиеРДС Цикл
		
		Отбор.Наименование = Элемент.Ключ;
		СтрокиСлоги = Слоги.НайтиСтроки(Отбор);
		Если СтрокиСлоги.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		СлогОбъект = Справочники.Слоги.СоздатьЭлемент();
		СлогОбъект.Наименование = Элемент.Ключ;
		СлогОбъект.НаименованиеНаРусском = СоответствиеРК.Получить(Элемент.Ключ);
		Для Каждого Моджи Из Элемент.Значение Цикл
			МоджиСсылка = НайтиМоджи(Моджи.Наименование, Ложь, Моджи.ЭтоМиниСимвол);
			Если НЕ ЗначениеЗаполнено(МоджиСсылка) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Не найден моджи " + Моджи.Наименование;
				Сообщение.Сообщить();
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			НоваяСтрока = СлогОбъект.Состав.Добавить();
			НоваяСтрока.Моджи = МоджиСсылка;
			
			МоджиСсылка = НайтиМоджи(Моджи.Наименование, Истина, Моджи.ЭтоМиниСимвол);
			Если НЕ ЗначениеЗаполнено(МоджиСсылка) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Не найден моджи катаканы " + Моджи.Наименование;
				Сообщение.Сообщить();
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			НоваяСтрока = СлогОбъект.СоставКатакана.Добавить();
			НоваяСтрока.Моджи = МоджиСсылка;
			
		КонецЦикла;
		СлогОбъект.Записать();
		
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМоры(Команда)
	ЗаполнитьСлогиНаСервере();
КонецПроцедуры

&НаСервере
Функция НайтиМоджи(Наименование, ЭтоКатакана, ЭтоМиниСимвол)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	НЕ Моджи.ПометкаУдаления
	|	И Моджи.Наименование = &Наименование
	|	И Моджи.ЭтоКатакана = &ЭтоКатакана
	|	И Моджи.ЭтоМиниСимвол = &ЭтоМиниСимвол";
	
	Запрос.УстановитьПараметр("Наименование", ПростыеТипыКлиентСервер.ВНРег(Наименование));
	Запрос.УстановитьПараметр("ЭтоКатакана", ЭтоКатакана);
	Запрос.УстановитьПараметр("ЭтоМиниСимвол", ЭтоМиниСимвол);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.Моджи.ПустаяСсылка();	
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

&НаСервере
Функция Слоги()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Слоги.Ссылка КАК Ссылка,
	|	Слоги.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Слоги КАК Слоги
	|ГДЕ
	|	Слоги.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет справочник "Слоги" на основе соответствий ромаджи-кириллицы и ромаджи-данным слогов.
// TODO - проверить что слоги уже созданы
Процедура ЗаполнитьСлоги() Экспорт
	
	СоответствиеРК = АзбукаВызовСервера.СоответствиеРомаджиКириллицы();
	СоответствиеРДС = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	Слоги = Слоги();
	
	ТаблицаМоджи = ТаблицаМоджи(Слоги, СоответствиеРДС);
	
	НачатьТранзакцию();
	Для Каждого Элемент Из СоответствиеРДС Цикл
		
		Отбор = Новый Структура("Наименование", Элемент.Ключ);
		СтрокиСлоги = Слоги.НайтиСтроки(Отбор);
		Если СтрокиСлоги.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		СлогОбъект = Справочники.Слоги.СоздатьЭлемент();
		СлогОбъект.Наименование = Элемент.Ключ;
		СлогОбъект.НаименованиеНаРусском = СоответствиеРК.Получить(Элемент.Ключ);
		Для Каждого Моджи Из Элемент.Значение Цикл
			МоджиСсылка = НайтиМоджиВТаблице(ТаблицаМоджи, Моджи, Ложь);
			Если НЕ ЗначениеЗаполнено(МоджиСсылка) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Не найден моджи хираганы " + Моджи.Наименование;
				Сообщение.Сообщить();
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			НоваяСтрока = СлогОбъект.Состав.Добавить();
			НоваяСтрока.Моджи = МоджиСсылка;
			
			МоджиСсылка = НайтиМоджиВТаблице(ТаблицаМоджи, Моджи, Истина);
			Если НЕ ЗначениеЗаполнено(МоджиСсылка) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Не найден моджи катаканы " + Моджи.Наименование;
				Сообщение.Сообщить();
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			НоваяСтрока = СлогОбъект.СоставКатакана.Добавить();
			НоваяСтрока.Моджи = МоджиСсылка;
			
		КонецЦикла;
		СлогОбъект.Записать();
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьСсылкиМоджи(ТаблицаМоджи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Ссылка КАК Ссылка,
	|	Моджи.Наименование КАК Наименование,
	|	Моджи.ЭтоКатакана КАК ЭтоКатакана,
	|	Моджи.ЭтоМиниСимвол КАК ЭтоМиниСимвол
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	НЕ Моджи.ПометкаУдаления
	|	И (Моджи.Наименование, Моджи.ЭтоКатакана, Моджи.ЭтоМиниСимвол) В
	|		(ВЫБРАТЬ
	|			Т.Наименование,
	|			Т.ЭтоКатакана,
	|			Т.ЭтоМиниСимвол
	|		ИЗ
	|			&ТаблицаМоджи КАК Т)";
	
	Запрос.УстановитьПараметр("ТаблицаМоджи", ТаблицаМоджи);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция НайтиМоджиВТаблице(ТаблицаМоджи, Моджи, ЭтоКатакана)
	
	СтрокиТаблицы = ТаблицаМоджи.НайтиСтроки(
		Новый Структура("Наименование, ЭтоКатакана, ЭтоМиниСимвол", Моджи.Наименование, ЭтоКатакана, Моджи.ЭтоМиниСимвол));
	Если СтрокиТаблицы.Количество() Тогда
		Возврат СтрокиТаблицы[0].Ссылка;
	КонецЕсли;
	Возврат Справочники.Моджи.ПустаяСсылка();
КонецФункции

Функция ТаблицаМоджи(ТаблицаСлоги, СоответствиеРДС)
	
	ТаблицаМоджи = Новый ТаблицаЗначений;
	ТаблицаМоджи.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.Моджи"));
	ТаблицаМоджи.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ТаблицаМоджи.Колонки.Добавить("ЭтоКатакана", Новый ОписаниеТипов("Булево"));
	ТаблицаМоджи.Колонки.Добавить("ЭтоМиниСимвол", Новый ОписаниеТипов("Булево"));
	
	Для Каждого Элемент Из СоответствиеРДС Цикл
		
		Отбор = Новый Структура("Наименование", Элемент.Ключ);
		СтрокиСлоги = ТаблицаСлоги.НайтиСтроки(Отбор);
		Если СтрокиСлоги.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Моджи Из Элемент.Значение Цикл
			
			СтрокаДляПоиска = ТаблицаМоджи.Добавить();
			СтрокаДляПоиска.Наименование = Моджи.Наименование;
			СтрокаДляПоиска.ЭтоКатакана = Ложь;
			СтрокаДляПоиска.ЭтоМиниСимвол = Моджи.ЭтоМиниСимвол;
		
			СтрокаДляПоиска2 = ТаблицаМоджи.Добавить();
			СтрокаДляПоиска2.Наименование = Моджи.Наименование;
			СтрокаДляПоиска2.ЭтоКатакана = Истина;
			СтрокаДляПоиска2.ЭтоМиниСимвол = Моджи.ЭтоМиниСимвол;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ЗаполнитьСсылкиМоджи(ТаблицаМоджи);
КонецФункции

Функция Слоги()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Слоги.Ссылка КАК Ссылка,
	|	Слоги.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Слоги КАК Слоги
	|ГДЕ
	|	Слоги.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецОбласти
#Иначе 
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
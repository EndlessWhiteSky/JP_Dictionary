#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Дозаполнение(Команда)
	ДозаполнениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДозаполнениеХираганы(Команда)
	ЗаполнениеХираганыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДозаполнениеКатаканой(Команда)
	ДозаполнениеКатаканойНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДозаполнениеМиниВерсиями(Команда)
	ДозаполнениеМиниСимволамиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДозаполнениеНаСервере()
	ЗаполнениеХираганыНаСервере();
	ДозаполнениеКатаканойНаСервере();
	ДозаполнениеМиниСимволамиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеХираганыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Ссылка КАК Ссылка,
	|	Моджи.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	Моджи.ПометкаУдаления = ЛОЖЬ
	|	И Моджи.ЭтоКатакана = ЛОЖЬ
	|	И Моджи.ЭтоМиниСимвол = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	СоответствиеРК = АзбукаВызовСервера.СоответствиеРомаджиКириллицы();
	СоответствиеРЮ = АзбукаВызовСервера.СоответствиеРомаджиЮникоду();
	
	Пока Выборка.Следующий() Цикл
		
		Моджи = Выборка.Ссылка.ПолучитьОбъект();
		НаименованиеНрег = СоответствиеРК.Получить(НРег(Выборка.Наименование));
		
		Моджи.НаименованиеНаРусском = ВРег(Лев(НаименованиеНрег,1)) + Сред(НаименованиеНрег,2);
		Юникод = СоответствиеРЮ.Получить(НРег(Выборка.Наименование)).Юникод;
		Моджи.Юникод = ЧислоИзШестнадцатеричнойСтроки(Юникод);
		Моджи.ЮникодПредставление = Символ(Моджи.Юникод);
		Моджи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДозаполнениеКатаканойНаСервере()
	
	Катакана = Катакана_Моджи();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Наименование КАК Наименование,
	|	Моджи.НаименованиеНаРусском КАК НаименованиеНаРусском
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	Моджи.ПометкаУдаления = ЛОЖЬ
	|	И Моджи.ЭтоКатакана = ЛОЖЬ
	|	И Моджи.ЭтоМиниСимвол = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Моджи.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	СоответствиеРЮ = АзбукаВызовСервера.СоответствиеРомаджиЮникоду();
	
	Пока Выборка.Следующий() Цикл
		
		Существующий = Катакана.Найти(Выборка.Наименование);
		Если Существующий <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Моджи = Справочники.Моджи.СоздатьЭлемент();
		Моджи.Наименование = Выборка.Наименование;
		Моджи.НаименованиеНаРусском = Выборка.НаименованиеНаРусском;
		Моджи.ЭтоКатакана = Истина;
		Юникод = СоответствиеРЮ.Получить(НРег(Выборка.Наименование)).ЮникодКатакана;
		Моджи.Юникод = ЧислоИзШестнадцатеричнойСтроки(Юникод);
		Моджи.ЮникодПредставление = Символ(Моджи.Юникод);
		Моджи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДозаполнениеМиниСимволамиНаСервере()
	
	МиниМоджи = Мини_Моджи();
	СоответствиеРЮ = АзбукаВызовСервера.СоответствиеРомаджиЮникоду();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Наименование КАК Наименование,
	|	Моджи.НаименованиеНаРусском КАК НаименованиеНаРусском,
	|	Моджи.ЭтоКатакана КАК ЭтоКатакана
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	Моджи.ПометкаУдаления = ЛОЖЬ
	|	И Моджи.ЭтоМиниСимвол = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Моджи.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Существующий = МиниМоджи.Найти(Выборка.Наименование);
		Если Существующий <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Ромаджи = Нрег(Выборка.Наименование);
		Данные = СоответствиеРЮ.Получить(Ромаджи);
			
		Если Выборка.ЭтоКатакана Тогда
			ЕстьМини = Данные.ЮникодМиниКатакана <> Неопределено;
			ЮникодМини = Данные.ЮникодМиниКатакана;
		Иначе
			ЕстьМини = Данные.ЮникодМини <> Неопределено;	
			ЮникодМини = Данные.ЮникодМини;
		КонецЕсли;
		Если ЕстьМини = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		Моджи = Справочники.Моджи.СоздатьЭлемент();
		Моджи.Наименование = Выборка.Наименование;
		Моджи.НаименованиеНаРусском = Выборка.НаименованиеНаРусском;
		Моджи.ЭтоКатакана = Выборка.ЭтоКатакана;
		Моджи.ЭтоМиниСимвол = Истина;
		Моджи.Юникод = ЧислоИзШестнадцатеричнойСтроки(ЮникодМини);
		Моджи.ЮникодПредставление = Символ(Моджи.Юникод);
		Моджи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция Катакана_Моджи()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	Моджи.ПометкаУдаления = ЛОЖЬ
	|	И Моджи.ЭтоКатакана = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Моджи = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Моджи.Добавить(Выборка.Наименование);
	КонецЦикла;
	Возврат Моджи;
	
КонецФункции

&НаСервере
Функция Мини_Моджи()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Моджи.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Моджи КАК Моджи
	|ГДЕ
	|	Моджи.ПометкаУдаления = ЛОЖЬ
	|	И Моджи.ЭтоМиниСимвол = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Моджи = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Моджи.Добавить(Выборка.Наименование);
	КонецЦикла;
	Возврат Моджи;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Проверяет наличие реквизита или свойства у произвольного объекта без обращения к метаданным.
//
// Параметры:
//  Объект       - Произвольный - объект, у которого нужно проверить наличие реквизита или свойства;
//  ИмяРеквизита - Строка       - имя реквизита или свойства.
//
// Возвращаемое значение:
//  Булево - Истина, если есть.
//
Функция ЕстьРеквизитИлиСвойствоОбъекта(Знач Объект, Знач ИмяРеквизита) Экспорт
    
    КлючУникальности   = Новый УникальныйИдентификатор;
    СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
    ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Объект);
    
    Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
    
КонецФункции

// Возвращает значение реквизита формы по указанному пути. Рекурсивно обходит вложенные объекты.
//
// Параметры:
//   Форма - Форма - Форма или объект, содержащий реквизит
//   ПутьКРеквизиту - Строка - Путь к реквизиту в формате "Реквизит1.Реквизит2"
//
// Возвращаемое значение:
//  Произвольный - Значение реквизита или Неопределено, если реквизит не найден
Функция ЗначениеРеквизитаФормы(Знач Форма, Знач ПутьКРеквизиту) Экспорт

	// Если реквизит имеет вид через-точку, то отделим первую часть
	// и будем работать с ней как с именем реквизита формы.
	ПозицияТочки = СтрНайти(ПутьКРеквизиту, ".");
	Если ПозицияТочки = 0 Тогда
		Возврат ?(ЕстьРеквизитИлиСвойствоОбъекта(Форма, ПутьКРеквизиту), Форма[ПутьКРеквизиту], Неопределено);
	КонецЕсли;
	Если ПозицияТочки = 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяРеквизита = Лев(ПутьКРеквизиту, ПозицияТочки - 1);
	Если НЕ ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РеквизитДляПоискаВнутрь = Форма[ИмяРеквизита];
	ПутьКРеквизиту = Сред(ПутьКРеквизиту, ПозицияТочки + 1);
	
	Возврат ЗначениеРеквизитаФормы(РеквизитДляПоискаВнутрь, ПутьКРеквизиту);
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

// При создании на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ПараметрыРаботыСДублями - Структура - см. Шаблон_ПараметрыРаботыСДублями()
Процедура ПриСозданииНаСервере(Форма, ПараметрыРаботыСДублями = Неопределено) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	// Создать в группе "ГруппаДубли" элементы управления для отображения дублей
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыРаботыСДублями") Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПараметрыРаботыСДублями", Новый ОписаниеТипов()));
	КонецЕсли;
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ИнформацияОДублях") Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИнформацияОДублях", Новый ОписаниеТипов()));
	КонецЕсли;
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Если Форма.ПараметрыРаботыСДублями = Неопределено Тогда
		Форма.ПараметрыРаботыСДублями = Шаблон_ПараметрыРаботыСДублями();
		Если ПараметрыРаботыСДублями <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Форма.ПараметрыРаботыСДублями, ПараметрыРаботыСДублями);
		КонецЕсли;
	КонецЕсли;
	
	ИмяГруппы = Форма.ПараметрыРаботыСДублями.ИмяГруппыДубли;
	НовыйЭлемент = Форма.Элементы.Добавить("НадписьДубли", Тип("ДекорацияФормы"), Форма.Элементы[ИмяГруппы]);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Гиперссылка = Истина;
	НовыйЭлемент.Заголовок = Форма.ПараметрыРаботыСДублями.ПодписьДубля;
	НовыйЭлемент.Видимость = Истина;
	НовыйЭлемент.Шрифт = ШрифтыСтиля.ЗаголовокПятогоУровня;
	
	НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_НадписьДублиНажатие");
	//  
	
КонецПроцедуры

// Поиск дублей.
// 
// Параметры:
//  ПараметрыРаботыСДублями - Структура - См. Шаблон_ПараметрыРаботыСДублями()
// 
// Возвращаемое значение:
//  Структура - Поиск дублей:
// * ЕстьДубли - Булево - Истина если найдены дубли
// * ИнформацияОДублях - Произвольный, Неопределено - информация о дублях
Функция ПоискДублей(ПараметрыРаботыСДублями) Экспорт
	
	Результат = Шаблон_ИнформацияОДублях();
	Запрос = Новый Запрос;
	Запрос.Текст = ПараметрыРаботыСДублями.ТекстЗапросаПоискДублей;
	Для Каждого ПараметрЗапроса Из ПараметрыРаботыСДублями.ПараметрыЗапросаПоискДублей Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаДубли = РезультатЗапроса.Выгрузить();
	Если ТаблицаДубли.Количество() > 0 Тогда
		Результат.ЕстьДубли = Истина;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Шаблон информация о дублях.
// 
// Возвращаемое значение:
//  Структура - Шаблон информация о дублях:
// * ЕстьДубли - Булево - Истина если найдены дубли
// * ИнформацияОДублях - Произвольный, Неопределено - информация о дублях
Функция Шаблон_ИнформацияОДублях() Экспорт
	
	ИнформацияОДублях = Новый Структура;
	ИнформацияОДублях.Вставить("ЕстьДубли", Ложь);
	ИнформацияОДублях.Вставить("ИнформацияОДублях", Неопределено);
	Возврат ИнформацияОДублях;
	
КонецФункции

// Шаблон параметры работы с дублями.
// 
// Возвращаемое значение:
//  Структура - Шаблон параметры работы с дублями:
// * ИмяГруппыДубли - Строка - имя элемента формы в котором размещаются элементы управления
// * ПодписьДубля - Строка - надпись на форму
// * ТекстЗапросаПоискДублей - Строка - текст запроса для поиска дублей
// * ПараметрыЗапросаПоискДублей - Структура - параметры запроса, ключ = имя параметра, значение = значение
Функция Шаблон_ПараметрыРаботыСДублями() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяГруппыДубли", "ГруппаДубли");
	Параметры.Вставить("ПодписьДубля", "В базе существуют другие элементы, которые могут быть дублями текущего");
	Параметры.Вставить("ТекстЗапросаПоискДублей", "ВЫБРАТЬ 1 КАК Поле");
	Параметры.Вставить("ПараметрыЗапросаПоискДублей", Новый Структура);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Преобразует первую букву строки в заглавную, а остальные - в строчные.
//
// Параметры:
//   Строка - Строка - Исходная строка для преобразования
//
// Возвращаемое значение:
//   Строка - Строка с первой заглавной буквой и остальными строчными
Функция ВНРег(Строка) Экспорт
	
	Возврат Врег(Лев(Строка,1)) + НРег(Сред(Строка,2));
	
КонецФункции

// Проверяет, начинается ли строка с префикса кода символа Юникода ("0x" или "U+").
//
// Параметры:
//  Строка - Строка - Строка для проверки.
//
// Возвращаемое значение:
//  Булево - Истина, если строка начинается с префикса, иначе Ложь.
//
Функция ЕстьПрефиксЮникода(Строка) Экспорт
	
	Если Лев(Строка, 2) = "0x"
	ИЛИ  Лев(Строка, 2) = "U+"
	Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Преобразует строковое представление юникод-символа в десятичное число.
//
// Параметры:
//   Строка - Строка - строковое представление юникод-символа с префиксом "U+" или без него.
//
// Возвращаемое значение:
//   Число - десятичное представление юникод-символа.
Функция ЮникодВДесятичное(Знач Строка) Экспорт
	
	Если ЕстьПрефиксЮникода(Строка) Тогда
		Строка = "0x" + Сред(Строка, 3);
	Иначе
		Строка = "0x" + Строка;
	КонецЕсли;
	
	Возврат ЧислоИзШестнадцатеричнойСтроки(Строка);
	
КонецФункции

// Преобразует десятичное число в его шестнадцатеричное представление в формате Юникода.
//
// Параметры:
//   Число - Число - Десятичное число для преобразования.
//
// Возвращаемое значение:
//   Строка - Шестнадцатеричное представление числа с префиксом "0x".
Функция ДесятичноеВЮникод(Число) Экспорт
	
	Буфер = Новый БуферДвоичныхДанных(4, ПорядокБайтов.BigEndian);
	Буфер.ЗаписатьЦелое32(0, Число);
	Возврат "0x" + ПолучитьHexСтрокуИзБуфераДвоичныхДанных(Буфер);
	
КонецФункции

#КонецОбласти
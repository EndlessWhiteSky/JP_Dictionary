#Область ПрограммныйИнтерфейс

// Проверяет, содержит ли коллекция токенов хотя бы один мусорный токен.
//
// Параметры:
//   Токены - Массив из Структура - Коллекция токенов для проверки
//
// Возвращаемое значение:
//   Булево - Истина, если найден хотя бы один мусорный токен
Функция ЕстьМусорныеТокены(Токены) Экспорт
	
	Для Каждого Токен Из Токены Цикл
		
		Если Токен.ЭтоМусор Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Разбивает строку на токены в соответствии с указанным словарем, выделяя известные слова и "мусорные" последовательности символов.
//
// Параметры:
//   Строка - Строка - Исходная строка для разбиения на токены
//   Словарь - Соответствие из Строка, Неопределено - Словарь для поиска токенов, по умолчанию используется словарь ромаджи
//
// Возвращаемое значение:
//   Массив из Структура - Массив структур с информацией о токенах (Представление, Данные, ЭтоМусор)
Функция Токенизация(Знач Строка, Словарь = Неопределено) Экспорт
	
	Если Словарь = Неопределено Тогда
		Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	КонецЕсли;
	
	// 1. Предобработка строки - удаление ненужных символов
	Строка = Токенизация_ПредобработкаСтроки(Строка);
	
	// 2. Подготовка
	Результат = Новый Массив;
	ДлинаСтроки = СтрДлина(Строка);
	ТекущаяПозиция = 1;
	МаксДлинаСлова = АзбукаКлиентСервер.МаксимальнаяДлинаКлючаСловарей();
	Мусор = "";
	
	// 3. Основной алгоритм разбиения
	Пока ТекущаяПозиция <= ДлинаСтроки Цикл
		
		НайденоСовпадение = Ложь;
		Длина = МаксДлинаСлова;
		// Ищем самое длинное совпадение от текущей позиции
		Пока Длина > 0 Цикл
			Конец = ТекущаяПозиция + Длина - 1;
			Если Конец > ДлинаСтроки Тогда
				Длина = Длина - 1;
				Продолжить;
			КонецЕсли;
			
			Подстрока = Сред(Строка, ТекущаяПозиция, Длина);
			ЗначениеВСловаре = Словарь.Получить(Подстрока);
			Если ЗначениеВСловаре <> Неопределено Тогда
				
				// Если есть накопленный мусор - добавляем его в результат
				ДобавитьМусорВРезультат(Результат, Мусор);
					
				// Нашли ключевое слово - добавляем в результат
				Результат.Добавить(СтруктураДанныеТокена(Подстрока, ЗначениеВСловаре, Ложь));
				ТекущаяПозиция = ТекущаяПозиция + Длина;
				НайденоСовпадение = Истина;
				Прервать;
			КонецЕсли;
			Длина = Длина - 1;
		КонецЦикла; 
		
		Если НЕ НайденоСовпадение Тогда
			ТекущийСимвол = Сред(Строка, ТекущаяПозиция, 1);
			Если ТекущийСимвол = "-" Тогда
				ДобавитьМусорВРезультат(Результат, Мусор);
			ИначеЕсли ТекущийСимвол <> "-" Тогда
				Мусор = Мусор + ТекущийСимвол;
			КонецЕсли;
            
            ТекущаяПозиция = ТекущаяПозиция + 1;
		КонецЕсли;
	КонецЦикла;
	
	// Если есть накопленный мусор - добавляем его в результат
	ДобавитьМусорВРезультат(Результат, Мусор);
	
				
	Возврат Результат;
	
КонецФункции

// Сравнить наборы токенов.
// 
// Параметры:
//  Токены1 - Массив из Структура - Массив №1
//  Токены2 - Массив из Структура - Массив №2
// 
// Возвращаемое значение:
//  Структура - Сравнить наборы токенов:
// * ЭтоРедакция - Булево - Истина если массив 2 можно получить из массива 1, добавив либо удалив ровно с одного края
// * ИндексСовпадающейЧасти1 - Число - Индекс начала совпадающей части в массиве 1
// * ИндексСовпадающейЧасти2 - Число - Индекс начала совпадающей части в массиве 2
// * ДлинаСовпадающейЧасти - Число - Длина совпадающей части, равна длине одного из массивов
Функция СравнитьНаборыТокенов(Знач Токены1, Знач Токены2) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоРедакция", Ложь);
	Результат.Вставить("ИндексСовпадающейЧасти1", -1);
	Результат.Вставить("ИндексСовпадающейЧасти2", -1);
	Результат.Вставить("ДлинаСовпадающейЧасти", 0);
	
	Проверка_ПрефиксСлева = ЭтоПрефикс(Токены1, Токены2);
	Проверка_ПрефиксСправа = ЭтоПрефикс(Токены2, Токены1);
	
	Проверка_Суффикс_Слева = ЭтоСуффикс(Токены1, Токены2);
	Проверка_Суффикс_Справа = ЭтоСуффикс(Токены2, Токены1);
	
	Если Проверка_ПрефиксСлева Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = 0;
		Результат.ИндексСовпадающейЧасти2 = 0;
		Результат.ДлинаСовпадающейЧасти = Токены1.Количество();
	ИначеЕсли Проверка_ПрефиксСправа Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = 0;
		Результат.ИндексСовпадающейЧасти2 = 0;
		Результат.ДлинаСовпадающейЧасти = Токены2.Количество();
	ИначеЕсли Проверка_Суффикс_Слева Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = 0;
		Результат.ИндексСовпадающейЧасти2 = Токены2.Количество() - Токены1.Количество();
		Результат.ДлинаСовпадающейЧасти = Токены1.Количество();
	ИначеЕсли Проверка_Суффикс_Справа Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = Токены1.Количество() - Токены2.Количество();
		Результат.ИндексСовпадающейЧасти2 = 0;
		Результат.ДлинаСовпадающейЧасти = Токены2.Количество();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Подбирает канджи, которые могли бы соответствовать набору токенов.
//
// Параметры:
//   Токены - Массив из Структура - Набор токенов для поиска канджи
//
// Возвращаемое значение:
//   Массив из Структура - Найденные канджи
Функция ПодобратьКанджи(Знач Токены) Экспорт
	
	Возврат РаботаСоСтрокамиВызовСервера.ПодобратьКанджи(Токены);
	
КонецФункции

// Шаблон структуры результат токенизации.
// 
// Возвращаемое значение:
//  Структура - Шаблон структуры результат токенизации:
// * Представление - Строка - представление токена
// * Данные - Неопределено - данные из словаря токенов
// * ЭтоМусор - Булево - признак мусорного токена
Функция ШаблонСтруктурыДанныеТокена() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Представление", "");
	Результат.Вставить("Данные",        Неопределено);
	Результат.Вставить("ЭтоМусор",      Ложь);
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет токен-мусор в массив-результат, если он не пустой, и очищает переменную мусора.
//
// Параметры:
//   Результат - Массив из Структура - см. ШаблонСтруктурыДанныеТокена()
//   Мусор - Строка - Накопленная строка с "мусором".
Процедура ДобавитьМусорВРезультат(Результат, Мусор)
	Если Мусор <> "" Тогда
		Результат.Добавить(СтруктураДанныеТокена(Мусор, Неопределено, Истина));
		Мусор = "";
	КонецЕсли;
КонецПроцедуры

// Выполняет предобработку строки: удаление или замена служебных символов.
//
// Параметры:
//   ИсходнаяСтрока - Строка - Строка для предобработки.
//
// Возвращаемое значение:
//   Строка - Обработанная строка.
Функция Токенизация_ПредобработкаСтроки(Знач ИсходнаяСтрока)
	
	ОбработаннаяСтрока = СтрЗаменить(ИсходнаяСтрока, " ", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, ",", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, ";", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, "_", "sokuon");
	
	Возврат ОбработаннаяСтрока;
	
КонецФункции

// Метод для создания структуры токена
//
// Параметры:
//   Представление - Строка - Текстовое представление токена.
//   Данные - Произвольный - Данные, связанные с токеном (например, слог).
//   ЭтоМусор - Булево - Истина, если токен является "мусорным".
//
// Возвращаемое значение:
//  Структура - Данные токена:
// * Представление - Строка - представление токена
// * Данные - Неопределено - данные из словаря токенов
// * ЭтоМусор - Булево - признак мусорного токена
Функция СтруктураДанныеТокена(Знач Представление, Знач Данные, Знач ЭтоМусор)
	СтруктураРезультат = ШаблонСтруктурыДанныеТокена();
	СтруктураРезультат.Представление = Представление;
	СтруктураРезультат.Данные = Данные;
	СтруктураРезультат.ЭтоМусор = ЭтоМусор;
	
	Возврат СтруктураРезультат;
КонецФункции

// Сравнивает два массива токенов, является ли первый префиксом второго
// 
// Параметры:
//  ТокеныПрефикс - Массив из Структура - Предполагаемый префикс
//  ТокеныПолное - Массив из Структура - Предполагаемый больший массив
// 
// Возвращаемое значение:
//  Булево - Это префикс
Функция ЭтоПрефикс(Знач ТокеныПрефикс, Знач ТокеныПолное)
	
	Если ТокеныПрефикс.Количество() > ТокеныПолное.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Счет = 0 По ТокеныПрефикс.ВГраница() Цикл
		Если ТокеныПрефикс[Счет].Представление <> ТокеныПолное[Счет].Представление Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;

КонецФункции

// Сравнивает два массива токенов, является ли первый суффиксом второго
// 
// Параметры:
//  ТокеныСуффикс - Массив из Структура - Предполагаемый Суффикс
//  ТокеныПолное - Массив из Структура - Предполагаемый больший массив
// 
// Возвращаемое значение:
//  Булево - Это префикс
Функция ЭтоСуффикс(Знач ТокеныСуффикс, Знач ТокеныПолное)
	
	Если ТокеныСуффикс.Количество() > ТокеныПолное.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СчетСуффикс = ТокеныСуффикс.ВГраница();
	СчетПолное =  ТокеныПолное.ВГраница();
	
	Пока СчетСуффикс >= 0 Цикл
		
		Если ТокеныСуффикс[СчетСуффикс].Представление <> ТокеныПолное[СчетПолное].Представление Тогда
			Возврат Ложь;
		КонецЕсли;
		
		СчетСуффикс = СчетСуффикс - 1;
		СчетПолное = СчетПолное - 1;
		
	КонецЦикла;
	
	Возврат Истина;

КонецФункции

#КонецОбласти
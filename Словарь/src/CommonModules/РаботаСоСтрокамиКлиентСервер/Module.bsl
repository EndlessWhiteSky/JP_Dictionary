#Область ПрограммныйИнтерфейс

// Проверяет, содержит ли коллекция токенов хотя бы один мусорный токен.
//
// Параметры:
//   Токены - Массив из Структура - Коллекция токенов для проверки
//
// Возвращаемое значение:
//   Булево - Истина, если найден хотя бы один мусорный токен
Функция ЕстьМусорныеТокены(Токены) Экспорт
	
	Для Каждого Токен Из Токены Цикл
		
		Если Токен.ЭтоМусор Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Разбивает строку на токены в соответствии с указанным словарем, выделяя известные слова и "мусорные" последовательности символов.
//
// Параметры:
//   Строка - Строка - Исходная строка для разбиения на токены
//   Словарь - Соответствие из Строка, Неопределено - Словарь для поиска токенов, по умолчанию используется словарь ромаджи
//
// Возвращаемое значение:
//   Массив из Структура - Массив структур с информацией о токенах (Представление, Данные, ЭтоМусор)
Функция Токенизация(Знач Строка, Словарь = Неопределено) Экспорт
	
	Если Словарь = Неопределено Тогда
		Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	КонецЕсли;
	
	// 1. Предобработка строки - удаление ненужных символов
	Строка = Токенизация_ПредобработкаСтроки(Строка);
	
	// 2. Подготовка
	Результат = Новый Массив;
	ДлинаСтроки = СтрДлина(Строка);
	ТекущаяПозиция = 1;
	МаксДлинаСлова = АзбукаКлиентСервер.МаксимальнаяДлинаКлючаСловарей();
	Мусор = "";
	
	// 3. Основной алгоритм разбиения
	Пока ТекущаяПозиция <= ДлинаСтроки Цикл
		
		НайденоСовпадение = Ложь;
		Длина = МаксДлинаСлова;
		// Ищем самое длинное совпадение от текущей позиции
		Пока Длина > 0 Цикл
			Конец = ТекущаяПозиция + Длина - 1;
			Если Конец > ДлинаСтроки Тогда
				Длина = Длина - 1;
				Продолжить;
			КонецЕсли;
			
			Подстрока = Сред(Строка, ТекущаяПозиция, Длина);
			ЗначениеВСловаре = Словарь.Получить(Подстрока);
			Если ЗначениеВСловаре <> Неопределено Тогда
				
				// Если есть накопленный мусор - добавляем его в результат
				ДобавитьМусорВРезультат(Результат, Мусор);
					
				// Нашли ключевое слово - добавляем в результат
				Результат.Добавить(СтруктураДанныеТокена(Подстрока, ЗначениеВСловаре, Ложь));
				ТекущаяПозиция = ТекущаяПозиция + Длина;
				НайденоСовпадение = Истина;
				Прервать;
			КонецЕсли;
			Длина = Длина - 1;
		КонецЦикла; 
		
		Если НЕ НайденоСовпадение Тогда
			ТекущийСимвол = Сред(Строка, ТекущаяПозиция, 1);
			Если ТекущийСимвол = "-" Тогда
				ДобавитьМусорВРезультат(Результат, Мусор);
			ИначеЕсли ТекущийСимвол <> "-" Тогда
				Мусор = Мусор + ТекущийСимвол;
			КонецЕсли;
            
            ТекущаяПозиция = ТекущаяПозиция + 1;
		КонецЕсли;
	КонецЦикла;
	
	// Если есть накопленный мусор - добавляем его в результат
	ДобавитьМусорВРезультат(Результат, Мусор);
	
				
	Возврат Результат;
	
КонецФункции

// Сравнить наборы токенов.
// 
// Параметры:
//  Токены1 - Массив из Структура - Массив №1
//  Токены2 - Массив из Структура - Массив №2
// 
// Возвращаемое значение:
//  Структура - Сравнить наборы токенов:
// * ЭтоРедакция - Булево - Истина если массив 2 можно получить из массива 1, добавив либо удалив ровно с одного края
// * ИндексСовпадающейЧасти1 - Число - Индекс начала совпадающей части в массиве 1
// * ИндексСовпадающейЧасти2 - Число - Индекс начала совпадающей части в массиве 2
// * ДлинаСовпадающейЧасти - Число - Длина совпадающей части, равна длине одного из массивов
Функция СравнитьНаборыТокенов(Знач Токены1, Знач Токены2) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоРедакция", Ложь);
	Результат.Вставить("ИндексСовпадающейЧасти1", -1);
	Результат.Вставить("ИндексСовпадающейЧасти2", -1);
	Результат.Вставить("ДлинаСовпадающейЧасти", 0);
	
	ОбщаяПрефикснаяЧасть = ОбщийПрефикс(Токены1, Токены2);
	Проверка_Общий_Префикс = ОбщаяПрефикснаяЧасть.Длина > 0;
	
	ОбщаяСуффикснаяЧасть = ОбщийСуффикс(Токены1, Токены2);
	Проверка_Общий_Суффикс = ОбщаяСуффикснаяЧасть.Длина > 0;
	
	Если Проверка_Общий_Префикс Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = 0;
		Результат.ИндексСовпадающейЧасти2 = 0;
		Результат.ДлинаСовпадающейЧасти = ОбщаяПрефикснаяЧасть.Длина;
	
	ИначеЕсли Проверка_Общий_Суффикс Тогда
		Результат.ЭтоРедакция = Истина;
		Результат.ИндексСовпадающейЧасти1 = Токены1.Количество() - ОбщаяСуффикснаяЧасть.Длина;
		Результат.ИндексСовпадающейЧасти2 = Токены2.Количество() - ОбщаяСуффикснаяЧасть.Длина;
		Результат.ДлинаСовпадающейЧасти = ОбщаяСуффикснаяЧасть.Длина;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Подбирает канджи, которые могли бы соответствовать набору токенов.
//
// Параметры:
//   Токены - Массив из Структура - Набор токенов для поиска канджи
//
// Возвращаемое значение:
//   Массив из Структура - Найденные канджи
Функция ПодобратьКанджи(Знач Токены) Экспорт
	
	Возврат РаботаСоСтрокамиВызовСервера.ПодобратьКанджи(Токены);
	
КонецФункции

// Шаблон структуры результат токенизации.
// 
// Возвращаемое значение:
//  Структура - Шаблон структуры результат токенизации:
// * Представление - Строка - представление токена
// * Данные - Неопределено - данные из словаря токенов
// * ЭтоМусор - Булево - признак мусорного токена
Функция ШаблонСтруктурыДанныеТокена() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Представление", "");
	Результат.Вставить("Данные",        Неопределено);
	Результат.Вставить("ЭтоМусор",      Ложь);
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет токен-мусор в массив-результат, если он не пустой, и очищает переменную мусора.
//
// Параметры:
//   Результат - Массив из Структура - см. ШаблонСтруктурыДанныеТокена()
//   Мусор - Строка - Накопленная строка с "мусором".
Процедура ДобавитьМусорВРезультат(Результат, Мусор)
	Если Мусор <> "" Тогда
		Результат.Добавить(СтруктураДанныеТокена(Мусор, Неопределено, Истина));
		Мусор = "";
	КонецЕсли;
КонецПроцедуры

// Ищем общий префикс двух массивов токенов
// 
// Параметры:
//  Токены1 - Массив из Структура - Массив №1
//  Токены2 - Массив из Структура - Массив №2
// 
// Возвращаемое значение:
//  Булево - Это префикс
Функция ОбщийПрефикс(Знач Токены1, Знач Токены2)
	
	Результат = Новый Структура("Префикс, Длина", "", 0);
	
	Позиция = 0;
	Пока Позиция < Токены1.Количество() И Позиция < Токены2.Количество() Цикл
		
		Если Токены1[Позиция].Представление <> Токены2[Позиция].Представление Тогда
			Прервать;
		КонецЕсли;
		
		Результат.Длина = Результат.Длина + 1;
		Результат.Префикс = Результат.Префикс + Токены1[Позиция].Представление;
		Позиция = Позиция + 1;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Ищем общий суффикс двух массивов токенов
// 
// Параметры:
//  Токены1 - Массив из Структура - Массив №1
//  Токены2 - Массив из Структура - Массив №2
// 
// Возвращаемое значение:
//  Булево - Это префикс
Функция ОбщийСуффикс(Знач Токены1, Знач Токены2)
	
	Результат = Новый Структура("Суффикс, Длина", "", 0);
	
	Позиция = 0;
	Пока Позиция < Токены1.Количество() И Позиция < Токены2.Количество() Цикл
		
		Если Токены1[Токены1.ВГраница() - Позиция].Представление <> Токены2[Токены2.ВГраница() - Позиция].Представление Тогда
			Прервать;
		КонецЕсли;
		
		Результат.Длина = Результат.Длина + 1;
		Результат.Суффикс = Токены1[Позиция].Представление + Результат.Суффикс;
		Позиция = Позиция + 1;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет предобработку строки: удаление или замена служебных символов.
//
// Параметры:
//   ИсходнаяСтрока - Строка - Строка для предобработки.
//
// Возвращаемое значение:
//   Строка - Обработанная строка.
Функция Токенизация_ПредобработкаСтроки(Знач ИсходнаяСтрока)
	
	ОбработаннаяСтрока = СтрЗаменить(ИсходнаяСтрока, " ", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, ",", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, ";", "");
	ОбработаннаяСтрока = СтрЗаменить(ОбработаннаяСтрока, "_", "sokuon");
	
	Возврат ОбработаннаяСтрока;
	
КонецФункции

// Метод для создания структуры токена
//
// Параметры:
//   Представление - Строка - Текстовое представление токена.
//   Данные - Произвольный - Данные, связанные с токеном (например, слог).
//   ЭтоМусор - Булево - Истина, если токен является "мусорным".
//
// Возвращаемое значение:
//  Структура - Данные токена:
// * Представление - Строка - представление токена
// * Данные - Неопределено - данные из словаря токенов
// * ЭтоМусор - Булево - признак мусорного токена
Функция СтруктураДанныеТокена(Знач Представление, Знач Данные, Знач ЭтоМусор)
	СтруктураРезультат = ШаблонСтруктурыДанныеТокена();
	СтруктураРезультат.Представление = Представление;
	СтруктураРезультат.Данные = Данные;
	СтруктураРезультат.ЭтоМусор = ЭтоМусор;
	
	Возврат СтруктураРезультат;
КонецФункции

#КонецОбласти
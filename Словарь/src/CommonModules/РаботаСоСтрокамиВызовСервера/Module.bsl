#Область ПрограммныйИнтерфейс

// По набору токенов подбирает подходящие кандзи.
//
// Параметры:
//   Токены - Массив из Структура - Массив токенов для анализа, см. РаботаСоСтрокамиКлиентСервер.ШаблонСтруктурыДанныеТокена
//
// Возвращаемое значение:
//   Массив из Структура - Массив структур с информацией о найденных кандзи (Канджи, Наименование, ЮникодПредставление)
Функция ПодобратьКанджи(Знач Токены) Экспорт
	Результат = Новый Массив;
	//1. Выделяем значимые токены
	ЗначимыеТокены = ЗначимыеТокены(Токены);
	Если ЗначимыеТокены.Количество() = 0 Тогда
		Возврат Результат;	
	КонецЕсли;
	
	ПотенциальныеКанджи = ПотенциальныеКанджи(ЗначимыеТокены);
	
	Если ПотенциальныеКанджи.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКанджиПоСпискуНаименований();
	Запрос.УстановитьПараметр("ПотенциальныеКанджи", ПотенциальныеКанджи);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураКанджи = Новый Структура("Канджи, Наименование, ЮникодПредставление");
		ЗаполнитьЗначенияСвойств(СтруктураКанджи, Выборка);
		Результат.Добавить(СтруктураКанджи);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// раскладывает токены собирает массив канджи по токенам с учетом оверлапов
Функция ПотенциальныеКанджи(МассивЦепочекТокенов)
	
	СловарьСлоговСДвойнымиГласными = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлоговСДвойнымиГласными();
	
	//2. Собираем массив всех возможных канджи слева направо
	ПотенциальныеКанджи = ШаблонТаблицы_ПотенциальныхКанджи();
	
	Для Каждого Токены Из МассивЦепочекТокенов Цикл	
		ДобавитьПотенциальныеКанджиПоМассивуТокенов(Токены, ПотенциальныеКанджи, СловарьСлоговСДвойнымиГласными);
	КонецЦикла;
	
	Возврат ПотенциальныеКанджи;
	
КонецФункции

// Генерирует потенциальные варианты канджи на основе массива токенов, учитывая особые правила японской письменности.
//
// Параметры:
//   Токены - Массив из Структура - Массив токенов для обработки, см. РаботаСоСтрокамиКлиентСервер.ШаблонСтруктурыДанныеТокена
//   ПотенциальныеКанджи - ТаблицаЗначений - см. ШаблонТаблицы_ПотенциальныхКанджи()
//   СловарьСлоговСДвойнымиГласными - Структура - Словарь слогов, содержащих двойные гласные
Процедура ДобавитьПотенциальныеКанджиПоМассивуТокенов(Токены, ПотенциальныеКанджи, СловарьСлоговСДвойнымиГласными)
	Для Счет = 0 По Токены.ВГраница() Цикл
		ПотенциальныйКанджи = "";
		Длина = 0;
		Для Индекс = Счет По Токены.ВГраница() Цикл
			
			Токен = Токены[Индекс];
			
			// Мусор
			Если Токен.Данные = Неопределено Или Токен.Данные.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
				
			// Если первый символ слога это сокуон докинем его к прошлому
			ПервыйСимвол = Токен.Данные[0];
			Если АзбукаВызовСервера.ЭтоСокуон(ПервыйСимвол.Наименование, ПервыйСимвол.ЭтоМиниСимвол) Тогда
				Если ПустаяСтрока(ПотенциальныйКанджи) Тогда
					Токен.Представление = Сред(Токен.Представление, 2);
				Иначе
					ПрошлыйКанджи = ПотенциальныеКанджи[ПотенциальныеКанджи.Количество() - 1];
					ДобавитьПотенциальныйКанджи(
						ПотенциальныеКанджи, ПрошлыйКанджи.Представление + "_", ПрошлыйКанджи.Длина);
				КонецЕсли;
			КонецЕсли;
				
			// технический токен включенный для уточнения предыдущего
			Если Токен.ЭтоМусор Тогда
				Прервать;
			КонецЕсли;
			
			Длина = Длина + 1;
			// Если последний символ слога это удвоенная гласная, докинем вариант без неё
			Если СловарьСлоговСДвойнымиГласными.Получить(Токен.Представление) <> Неопределено Тогда		
				ЛеваяЧасть = Лев(Токен.Представление, СтрДлина(Токен.Представление) - 1);
				ДобавитьПотенциальныйКанджи(ПотенциальныеКанджи, ПотенциальныйКанджи + ЛеваяЧасть, Длина);
			КонецЕсли;

			ПотенциальныйКанджи = ПотенциальныйКанджи + Токен.Представление;
			ДобавитьПотенциальныйКанджи(ПотенциальныеКанджи, ПотенциальныйКанджи, Длина);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьПотенциальныйКанджи(ПотенциальныеКанджи, Представление, Длина)
	
	НоваяСтрока = ПотенциальныеКанджи.Добавить();
	НоваяСтрока.Представление = Представление;
	НоваяСтрока.Длина = Длина;
	
КонецПроцедуры

// Группирует токены в подмассивы, исключая мусорные токены и добавляя значимые.
//
// Параметры:
//   Токены - Массив из Массив из Структура - Массив токенов для обработки, см. РаботаСоСтрокамиКлиентСервер.ШаблонСтруктурыДанныеТокена
//
// Возвращаемое значение:
//   Массив из Массив из Структура - Массив массивов значимых токенов
//@skip-check doc-comment-type
Функция ЗначимыеТокены(Токены)
	
	Счет = 0;
	
	ЗначимыеТокены = Новый Массив;
	
	Пока Счет < Токены.Количество() Цикл
		ПодМассив = Новый Массив;
		Пока Счет < Токены.Количество() И Токены[Счет].ЭтоМусор Цикл
			Счет = Счет + 1;
			Продолжить;
		КонецЦикла;
		
		Пока Счет < Токены.Количество() И НЕ Токены[Счет].ЭтоМусор Цикл
			ПодМассив.Добавить(Токены[Счет]);
			Счет = Счет + 1;
			Продолжить;
		КонецЦикла;
		
		// Если первый незначимый токен с удвоенной согласной, то добавим его тоже
		Если Счет < Токены.Количество() И Токены[Счет].Данные <> Неопределено Тогда
			ПервыйСимвол = Токены[Счет].Данные[0];
			Если АзбукаВызовСервера.ЭтоСокуон(ПервыйСимвол.Наименование, ПервыйСимвол.ЭтоМиниСимвол) Тогда
				ПодМассив.Добавить(Токены[Счет]);
			КонецЕсли;
		КонецЕсли;

		Если ПодМассив.Количество() > 0 Тогда
			ЗначимыеТокены.Добавить(ПодМассив);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначимыеТокены;
	
КонецФункции

// Возвращает текст запроса для поиска иероглифов канджи по списку наименований.
//
// Возвращаемое значение:
//   Строка - Текст запроса
Функция ТекстЗапросаКанджиПоСпискуНаименований()
	
	Возврат 
	"ВЫБРАТЬ
	|	Т.Представление КАК Представление,
	|	Т.Длина КАК Длина
	|ПОМЕСТИТЬ ВТ_ПотенциальныеКанджи
	|ИЗ
	|	&ПотенциальныеКанджи КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Представление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КанджиЧтения.НомерСтроки КАК НомерСтроки,
	|	КанджиЧтения.Ссылка КАК Канджи,
	|	ВТ_ПотенциальныеКанджи.Длина КАК Длина,
	|	ВТ_ПотенциальныеКанджи.Представление КАК Наименование
	|ПОМЕСТИТЬ ВТ_Канджи
	|ИЗ
	|	Справочник.Канджи.Чтения КАК КанджиЧтения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЧтенияКанджи КАК ЧтенияКанджи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПотенциальныеКанджи КАК ВТ_ПотенциальныеКанджи
	|			ПО ЧтенияКанджи.Наименование = ВТ_ПотенциальныеКанджи.Представление
	|		ПО КанджиЧтения.Чтение = ЧтенияКанджи.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Канджи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 12
	|	ВТ_Канджи.Канджи КАК Канджи,
	|	ВТ_Канджи.Наименование КАК Наименование,
	|	Спр_Канджи.ЮникодПредставление КАК ЮникодПредставление
	|ИЗ
	|	ВТ_Канджи КАК ВТ_Канджи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Канджи КАК Спр_Канджи
	|		ПО ВТ_Канджи.Канджи = Спр_Канджи.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Канджи.Длина УБЫВ,
	|	ВТ_Канджи.НомерСтроки";
	
КонецФункции

// Создает и возвращает таблицу значений для хранения потенциальных канджи
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Шаблон таблицы потенциальных канджи:
// * Представление - Строка - представление
// * Длина - Число - длина в слогах
Функция ШаблонТаблицы_ПотенциальныхКанджи()
	КвалификаторыСтроки = Новый КвалификаторыСтроки(10);
	ОписаниеСтроки = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);
	
	Шаблон = Новый ТаблицаЗначений;
	Шаблон.Колонки.Добавить("Представление", ОписаниеСтроки);
	Шаблон.Колонки.Добавить("Длина", Новый ОписаниеТипов("Число"));
	
	Возврат Шаблон;
КонецФункции

#КонецОбласти
// По набору токенов (см. Токенизация()) подбирает канджи которые могли бы подойти
Функция ПодобратьКанджи(Знач Токены) Экспорт
	Результат = Новый Массив;
	//1. Выделяем значимые токены
	ЗначимыеТокены = ЗначимыеТокены(Токены);
	Если ЗначимыеТокены.Количество() = 0 Тогда
		Возврат Результат;	
	КонецЕсли;
	
	ПотенциальныеКанджи = ПотенциальныеКанджи(ЗначимыеТокены);
	
	Если ПотенциальныеКанджи.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКанджиПоСпискуНаименований();
	Запрос.УстановитьПараметр("ПотенциальныеКанджи", ПотенциальныеКанджи);
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураКанджи = Новый Структура("Канджи, Наименование, ЮникодПредставление");
		ЗаполнитьЗначенияСвойств(СтруктураКанджи, Выборка);
		
		Результат.Добавить(СтруктураКанджи);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Массив массивов токенов, без мусора подряд
Функция ЗначимыеТокены(Токены)
	
	Счет = 0;
	
	ЗначимыеТокены = Новый Массив;
	
	Пока Счет < Токены.Количество() Цикл
		ПодМассив = Новый Массив;
		Пока Счет < Токены.Количество() И Токены[Счет].ЭтоМусор Цикл
			Счет = Счет + 1;
			Продолжить;
		КонецЦикла;
		
		Пока Счет < Токены.Количество() И НЕ Токены[Счет].ЭтоМусор Цикл
			ПодМассив.Добавить(Токены[Счет]);
			Счет = Счет + 1;
			Продолжить;
		КонецЦикла;
		
		// Если первый незначимый токен с удвоенной согласной, то добавим его тоже
		Если Счет < Токены.Количество() Тогда
			ПервыйСимвол = Токены[Счет].Данные[0];
			Если АзбукаВызовСервера.ЭтоСокуон(ПервыйСимвол.Наименование, ПервыйСимвол.ЭтоМиниСимвол) Тогда
				ПодМассив.Добавить(Токены[Счет]);
			КонецЕсли;
		КонецЕсли;

		Если ПодМассив.Количество() > 0 Тогда
			ЗначимыеТокены.Добавить(ПодМассив);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначимыеТокены;
	
КонецФункции

// раскладывает токены собирает массив канджи по токенам с учетом оверлапов
Функция ПотенциальныеКанджи(МассивЦепочекТокенов)
	
	СловарьСлоговСДвойнымиГласными = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлоговСДвойнымиГласными();
	
	//2. Собираем массив всех возможных канджи слева направо
	ПотенциальныеКанджи = Новый ТаблицаЗначений;
	КвалификаторыСтроки = Новый КвалификаторыСтроки(10);
    ОписаниеСтроки = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);
	ПотенциальныеКанджи.Колонки.Добавить("Представление", ОписаниеСтроки);
	ПотенциальныеКанджи.Колонки.Добавить("Длина", Новый ОписаниеТипов("Число"));
	
	Для Каждого Токены Из МассивЦепочекТокенов Цикл
		
		Для Счет = 0 По Токены.ВГраница() Цикл
			ПотенциальныйКанджи = "";
			Длина = 0;
			Для Индекс = Счет По Токены.ВГраница() Цикл
				Токен = Токены[Индекс];
				
				// Мусор
				Если Токен.Данные = Неопределено ИЛИ Токен.Данные.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				
				// Если первый символ слога это сокуон докинем его к прошлому
				ПервыйСимвол = Токен.Данные[0];
				Если АзбукаВызовСервера.ЭтоСокуон(ПервыйСимвол.Наименование, ПервыйСимвол.ЭтоМиниСимвол) Тогда
					
					Если ПустаяСтрока(ПотенциальныйКанджи) Тогда
						Токен.Представление = Сред(Токен.Представление, 2);
					Иначе
						НоваяСтрока = ПотенциальныеКанджи.Добавить();
						ПрошлыйКанджи = ПотенциальныеКанджи[ПотенциальныеКанджи.Количество() - 1];
						НоваяСтрока.Представление = ПрошлыйКанджи.Представление + "_";
						НоваяСтрока.Длина = ПрошлыйКанджи.Длина;
					КонецЕсли;
				КонецЕсли;
				
				// технический токен включенный для уточнения предыдущего
				Если Токен.ЭтоМусор Тогда
					Прервать;
				КонецЕсли;

				Длина = Длина + 1;
				// Если последний символ слога это удвоенная гласная, докинем вариант без неё
				Если СловарьСлоговСДвойнымиГласными.Получить(Токен.Представление) <> Неопределено Тогда
					НоваяСтрока = ПотенциальныеКанджи.Добавить();
					ЛеваяЧасть = Лев(Токен.Представление, СтрДлина(Токен.Представление) - 1);
					НоваяСтрока.Представление = ПотенциальныйКанджи + ЛеваяЧасть;
					НоваяСтрока.Длина = Длина;
				КонецЕсли;
				
				ПотенциальныйКанджи = ПотенциальныйКанджи + Токен.Представление;
				НоваяСтрока = ПотенциальныеКанджи.Добавить();
				НоваяСтрока.Представление = ПотенциальныйКанджи;
				НоваяСтрока.Длина = Длина;	
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПотенциальныеКанджи;
	
КонецФункции

Функция ТекстЗапросаКанджиПоСпискуНаименований() Экспорт
	
	Возврат 
	"ВЫБРАТЬ
	|	Т.Представление КАК Представление,
	|	Т.Длина КАК Длина
	|ПОМЕСТИТЬ ВТ_ПотенциальныеКанджи
	|ИЗ
	|	&ПотенциальныеКанджи КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Представление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КанджиЧтения.НомерСтроки КАК НомерСтроки,
	|	КанджиЧтения.Ссылка КАК Канджи,
	|	ВТ_ПотенциальныеКанджи.Длина КАК Длина,
	|	ВТ_ПотенциальныеКанджи.Представление КАК Наименование
	|ПОМЕСТИТЬ ВТ_Канджи
	|ИЗ
	|	Справочник.Канджи.Чтения КАК КанджиЧтения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЧтенияКанджи КАК ЧтенияКанджи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПотенциальныеКанджи КАК ВТ_ПотенциальныеКанджи
	|			ПО ЧтенияКанджи.Наименование = ВТ_ПотенциальныеКанджи.Представление
	|		ПО КанджиЧтения.Чтение = ЧтенияКанджи.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Канджи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 12
	|	ВТ_Канджи.Канджи КАК Канджи,
	|	ВТ_Канджи.Наименование КАК Наименование,
	|	Спр_Канджи.ЮникодПредставление КАК ЮникодПредставление
	|ИЗ
	|	ВТ_Канджи КАК ВТ_Канджи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Канджи КАК Спр_Канджи
	|		ПО ВТ_Канджи.Канджи = Спр_Канджи.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Канджи.Длина УБЫВ,
	|	ВТ_Канджи.НомерСтроки";
	
КонецФункции


&НаКлиенте
Перем Словарь;

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
	ЗаполнитьЧтения();
	ЗаполнитьСлова();
	Элементы.ЮникодПредставление.ПропускатьПриВводе = ЗначениеЗаполнено(Объект.ЮникодПредставление);
КонецПроцедуры

&НаКлиенте
Процедура ЮникодПриИзменении(Элемент)
	Если ПростыеТипыКлиентСервер.ЕстьПрефиксЮникода(Юникод) Тогда
		Объект.Юникод = ПростыеТипыКлиентСервер.ЮникодВДесятичное(Юникод);
		Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
	Иначе
		Объект.Юникод = Юникод;
	КонецЕсли;
	Объект.ЮникодПредставление = Символ(Объект.Юникод);
КонецПроцедуры

&НаКлиенте
Процедура ЮникодПредставлениеПриИзменении(Элемент)
	Объект.Юникод = КодСимвола(Объект.ЮникодПредставление);
	Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаименованиеНаСервере()
	
	СпрОбъект = РеквизитФормыВЗначение("Объект");
	СпрОбъект.ЗаполнитьНаименование();
	ЗначениеВРеквизитФормы(СпрОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтенияЧтениеСтрокаПриИзменении(Элемент)
	
	ОбновитьДанныеЧтения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтенияВидЧтенияПриИзменении(Элемент)
	
	ОбновитьДанныеЧтения();
	
КонецПроцедуры

// Ищет или создает ссылку на чтение по данным строки
&НаКлиенте
Процедура ОбновитьДанныеЧтения()
	
	ТекДанные = Элементы.Чтения.ТекущиеДанные;
	НомерСтроки = Элементы.Чтения.ТекущаяСтрока + 1;
	Если НЕ ЗначениеЗаполнено(ТекДанные.ВидЧтения) Тогда
		
		ТекДанные.ВидЧтения = ПредопределенноеЗначение("Перечисление.ВидыЧтенийКанджи.Кунное");
		
	КонецЕсли;
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(ТекДанные.ЧтениеСтрока, Словарь);
	Если РаботаСоСтрокамиКлиентСервер.ЕстьМусорныеТокены(Токены) Тогда
		Возврат; // TODO
	КонецЕсли;
	
	ТекДанные.ЧтениеСсылка = ДанныеЧтенияНаСервере(ТекДанные.ЧтениеСтрока, ТекДанные.ВидЧтения);
	Если НомерСтроки = 1 Тогда
		Объект.Наименование = ТекДанные.ЧтениеСтрока;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДанныеЧтенияНаСервере(ЧтениеСтрока, ВидЧтения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧтенияКанджи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЧтенияКанджи КАК ЧтенияКанджи
	|ГДЕ
	|	ЧтенияКанджи.ПометкаУдаления = ЛОЖЬ
	|	И ЧтенияКанджи.Наименование = &ЧтениеСтрока
	|	И ЧтенияКанджи.ВидЧтения = &ВидЧтения";
	Запрос.УстановитьПараметр("ЧтениеСтрока", ЧтениеСтрока);
	Запрос.УстановитьПараметр("ВидЧтения", ВидЧтения);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		НовоеЧтение = Справочники.ЧтенияКанджи.СоздатьЭлемент();
		ДанныеЗаполнения = Новый Структура("Наименование, ВидЧтения", ЧтениеСтрока, ВидЧтения);
		НовоеЧтение.Заполнить(ДанныеЗаполнения);
		НовоеЧтение.Записать();
		Возврат НовоеЧтение.Ссылка;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЧтения()
	
	Чтения.Очистить();
	Для Каждого Чтение Из Объект.Чтения Цикл
		
		НоваяСтрока = Чтения.Добавить();
		НоваяСтрока.ЧтениеСсылка = Чтение.Чтение;
		НоваяСтрока.ЧтениеСтрока = Строка(НоваяСтрока.ЧтениеСсылка);
		НоваяСтрока.ВидЧтения = Чтение.ВидЧтения;
		НоваяСтрока.Примечание = Чтение.Примечание;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлова()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	СловаПоКанджи.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СловарьСостав.Ссылка КАК Слово
	|ИЗ
	|	Справочник.Словарь.Состав КАК СловарьСостав
	|ГДЕ
	|	СловарьСостав.Канджи = &Канджи
	|
	|УПОРЯДОЧИТЬ ПО
	|	СловарьСостав.Ссылка.Наименование";
	Запрос.УстановитьПараметр("Канджи", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	СловаПоКанджи.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Чтения.Очистить();
	Для Каждого Чтение Из Чтения Цикл
		
		НоваяСтрока = Объект.Чтения.Добавить();
		НоваяСтрока.Чтение = Чтение.ЧтениеСсылка;
		НоваяСтрока.ВидЧтения = Чтение.ВидЧтения;
		НоваяСтрока.Примечание = Чтение.Примечание;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтенияЧтениеСтрокаОткрытие(Элемент, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Чтения.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекДанные.ЧтениеСсылка) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекДанные.ЧтениеСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСловарь(Команда)
	
	Записать();
	ПараметрыФормы = Новый Структура("Основание", Объект.Ссылка);
	ОткрытьФорму("Справочник.Словарь.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры



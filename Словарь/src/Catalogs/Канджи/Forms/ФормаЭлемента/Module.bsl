#Область ОписаниеПеременных

// Словарь, в котором хранятся соответствия ромаджи и данных слогов
&НаКлиенте
Перем Словарь;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	РаботаСДублямиКлиент.ПоискДублей(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
	ЗаполнитьЧтения();
	ЗаполнитьСлова();
	Элементы.ЮникодПредставление.ПропускатьПриВводе = ЗначениеЗаполнено(Объект.ЮникодПредставление);
	РаботаСДублямиСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРаботыСДублями());
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Чтения.Очистить();
	Для Каждого Чтение Из Чтения Цикл
		
		НоваяСтрока = Объект.Чтения.Добавить();
		НоваяСтрока.Чтение = Чтение.ЧтениеСсылка;
		НоваяСтрока.ВидЧтения = Чтение.ВидЧтения;
		НоваяСтрока.Примечание = Чтение.Примечание;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписьКанджи", Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ЗаписьСлова" Тогда
		ЗаполнитьСлова();
	КонецЕсли;
	 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЮникодПриИзменении(Элемент)
	Если ПростыеТипыКлиентСервер.ЕстьПрефиксЮникода(Юникод) Тогда
		Объект.Юникод = ПростыеТипыКлиентСервер.ЮникодВДесятичное(Юникод);
		Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
	Иначе
		Попытка
			Объект.Юникод = Число(Юникод);
		Исключение
			Объект.Юникод = 0;
		КонецПопытки;
	КонецЕсли;
	Объект.ЮникодПредставление = Символ(Объект.Юникод);
	РаботаСДублямиКлиент.ПоискДублей(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЮникодПредставлениеПриИзменении(Элемент)
	Объект.Юникод = КодСимвола(Объект.ЮникодПредставление);
	Юникод = ПростыеТипыКлиентСервер.ДесятичноеВЮникод(Объект.Юникод);
	РаботаСДублямиКлиент.ПоискДублей(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЧтения

&НаКлиенте
Процедура ЧтенияЧтениеСтрокаПриИзменении(Элемент)
	
	ОбновитьДанныеЧтения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтенияВидЧтенияПриИзменении(Элемент)
	
	ОбновитьДанныеЧтения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтенияЧтениеСтрокаОткрытие(Элемент, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Чтения.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекДанные.ЧтениеСсылка) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекДанные.ЧтениеСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВСловарь(Команда)
	
	Записать();
	ПараметрыФормы = Новый Структура("Основание", Объект.Ссылка);
	ОткрытьФорму("Справочник.Словарь.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Ищет или создает ссылку на чтение по данным строки
&НаКлиенте
Процедура ОбновитьДанныеЧтения()
	
	Модифицированность = Истина;
	ТекДанные = Элементы.Чтения.ТекущиеДанные;
	НомерСтроки = Элементы.Чтения.ТекущаяСтрока + 1;
	Если НЕ ЗначениеЗаполнено(ТекДанные.ВидЧтения) Тогда
		
		ТекДанные.ВидЧтения = ПредопределенноеЗначение("Перечисление.ВидыЧтенийКанджи.Кунное");
		
	КонецЕсли;
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(ТекДанные.ЧтениеСтрока, Словарь);
	Если РаботаСоСтрокамиКлиентСервер.ЕстьМусорныеТокены(Токены) Тогда
		Возврат; // TODO
	КонецЕсли;
	
	ТекДанные.ЧтениеСсылка = ДанныеЧтенияНаСервере(ТекДанные.ЧтениеСтрока, ТекДанные.ВидЧтения);
	Если НомерСтроки = 1 Тогда
		Объект.Наименование = ТекДанные.ЧтениеСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеЧтенияНаСервере(ЧтениеСтрока, ВидЧтения)
	
	Возврат Справочники.ЧтенияКанджи.ПолучитьОбновитьЧтение(ЧтениеСтрока, ВидЧтения);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЧтения()
	
	Чтения.Очистить();
	Для Каждого Чтение Из Объект.Чтения Цикл
		
		НоваяСтрока = Чтения.Добавить();
		НоваяСтрока.ЧтениеСсылка = Чтение.Чтение;
		НоваяСтрока.ЧтениеСтрока = Строка(НоваяСтрока.ЧтениеСсылка);
		НоваяСтрока.ВидЧтения = Чтение.ВидЧтения;
		НоваяСтрока.Примечание = Чтение.Примечание;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлова()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	СловаПоКанджи.Очистить();
	
	СловаПоКанджи.Загрузить(Справочники.Канджи.СловаПоКанджи(Объект.Ссылка));
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРаботыСДублями()
	
	ПараметрыРаботыСДублями = РаботаСДублямиСервер.Шаблон_ПараметрыРаботыСДублями();
	ПараметрыРаботыСДублями.ТекстЗапросаПоискДублей = Справочники.Канджи.ТекстЗапросаПоискДублей();
	ПараметрыЗапросаПоискДублей = Новый Структура;
	ПараметрыЗапросаПоискДублей.Вставить("Юникод", "Объект.Юникод");
	ПараметрыЗапросаПоискДублей.Вставить("Ссылка", "Объект.Ссылка");
	ПараметрыРаботыСДублями.ПараметрыЗапросаПоискДублей = ПараметрыЗапросаПоискДублей;
	Возврат ПараметрыРаботыСДублями;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_НадписьДублиНажатие()
	РаботаСДублямиКлиент.НадписьДублиНажатие(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

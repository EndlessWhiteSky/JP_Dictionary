#Область ОписаниеПеременных

&НаКлиенте
Перем ИдТекущегоЭлементаHTML; // Для определения элемента при нажатии ПКМ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицы();
	ЗаполнитьСписокВыбораПоляСортировки();
	ИнициализацияТаблицыНаФорме();
	ВидимостьРасширенногоПоиска = Истина;
	ВидимостьМенюСортировки = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьРасширенногоПоиска();
	УстановитьВидимостьМенюСортировки();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Отбор = Новый Структура("Ссылка", Параметр);
	// Если записан новый канджи, обновим таблицу
	Если ИмяСобытия = "ЗаписьКанджи" И ТаблицаКанджи.НайтиСтроки(Отбор).Количество() = 0 Тогда
		ТаблицаКанджиОбновитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ДанныеСобытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекЭлемент = ДанныеСобытия.Element;
	Если ТекЭлемент = Неопределено ИЛИ ТекЭлемент.classname = "" Тогда
		Возврат;
	КонецЕсли;
	
	Пока ТекЭлемент.parentelement <> Неопределено Цикл
		Если ТекЭлемент.classname = "card" Тогда
			Прервать;
		КонецЕсли;
		ТекЭлемент = ТекЭлемент.parentelement;
	КонецЦикла;
	Если ТекЭлемент.classname <> "card" Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем если это нажатие ПКМ, просто запоминаем id на потом
	Свойство_propertyName = Элементы.HTMLДокумент.Документ.getElementById("propertyName");
	Если Свойство_propertyName.innertext = "КОНТЕКСТНОЕ_МЕНЮ" Тогда
		ИдТекущегоЭлементаHTML = ТекЭлемент.id;
		Свойство_propertyName.innertext = "";
		Возврат;
	КонецЕсли;
	
	// Если ЛКМ, открываем
	Канджи = ТаблицаКанджи.НайтиСтроки(Новый Структура("Идентификатор", ТекЭлемент.id));
	Если Канджи.Количество() Тогда
		
		ОткрытьФормуКанджи(Канджи[0].Ссылка);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоискВидЧтенияПриИзменении(Элемент)
	ИнициализацияТаблицыНаФорме();
КонецПроцедуры

&НаКлиенте
Процедура ПолеСортировкиПриИзменении(Элемент)
	
	СортироватьКанджи();
	ИнициализацияТаблицыНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеСортировкиПриИзменении(Элемент)
	
	СортироватьКанджи();
	ИнициализацияТаблицыНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаПриИзменении(Элемент)
	ИнициализацияТаблицыНаФорме();
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаПоЧтениюПриИзменении(Элемент)
	ИнициализацияТаблицыНаФорме();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РасширенныйПоискПереключитьВидимость(Команда)
	
	ВидимостьРасширенногоПоиска = НЕ ВидимостьРасширенногоПоиска;
	
	УстановитьВидимостьРасширенногоПоиска();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКанджиОбновить(Команда)
	ТаблицаКанджиОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КанджиКопировать(Команда)
	
	Если НЕ ЗначениеЗаполнено(ИдТекущегоЭлементаHTML) Тогда
		Возврат;
	КонецЕсли;
	
	Канджи = ТаблицаКанджи.НайтиСтроки(Новый Структура("Идентификатор", ИдТекущегоЭлементаHTML));
	
	Если Канджи.Количество() Тогда
		КлючОткрытия = Канджи[0].Ссылка;
		ПараметрыФормы = Новый Структура("ЗначениеКопирования", КлючОткрытия);
		ОткрытьФорму("Справочник.Канджи.Форма.ФормаЭлемента", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанджиОткрыть(Команда)
	
	Если НЕ ЗначениеЗаполнено(ИдТекущегоЭлементаHTML) Тогда
		Возврат;
	КонецЕсли;
	
	Канджи = ТаблицаКанджи.НайтиСтроки(Новый Структура("Идентификатор", ИдТекущегоЭлементаHTML));
	
	Если Канджи.Количество() Тогда
		ОткрытьФормуКанджи(Канджи[0].Ссылка);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПереключитьВидимость(Команда)
	
	ВидимостьМенюСортировки = НЕ ВидимостьМенюСортировки;
	
	УстановитьВидимостьМенюСортировки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Канджи.Ссылка КАК Ссылка,
	|	Канджи.Код КАК Код,
	|	Канджи.Наименование КАК Наименование,
	|	Канджи.ОсновноеЗначение КАК ОсновноеЗначение,
	|	Канджи.ЮникодПредставление КАК Представление
	|ИЗ
	|	Справочник.Канджи КАК Канджи
	|ГДЕ
	|	Канджи.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаКанджи.Загрузить(РезультатЗапроса.Выгрузить());
	СортироватьКанджи();
	
	Для Счет = 0 По ТаблицаКанджи.Количество() - 1 Цикл
		
		ТаблицаКанджи[Счет].Идентификатор = Формат(Счет + 1, "ЧГ=");
		
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КанджиЧтения.Ссылка КАК Канджи,
	|	КанджиЧтения.Чтение.Наименование КАК Наименование,
	|	КанджиЧтения.Чтение.ЗаписьКаной КАК ЗаписьКаной,
	|	КанджиЧтения.ВидЧтения КАК ВидЧтения
	|ИЗ
	|	Справочник.Канджи.Чтения КАК КанджиЧтения
	|
	|УПОРЯДОЧИТЬ ПО
	|	КанджиЧтения.НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ЧтенияКанджи.Загрузить(РезультатЗапроса.Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПоляСортировки()
	
	Элементы.ПолеСортировки.СписокВыбора.Очистить();
	КолонкиИсключить = Новый Массив;
	КолонкиИсключить.Добавить("ПометкаУдаления");
	КолонкиИсключить.Добавить("Ссылка");
	КолонкиИсключить.Добавить("Идентификатор");
	
	КастомныеЗаголовки = Новый Соответствие;
	КастомныеЗаголовки.Вставить("ОсновноеЗначение", "Значение");
	КастомныеЗаголовки.Вставить("Представление", "Юникод");
	
	Для Каждого Колонка Из ТаблицаКанджи.Выгрузить().Колонки Цикл
		
		Если КолонкиИсключить.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КолонкаЗаголовок = КастомныеЗаголовки[Колонка.Имя];
		Если КолонкаЗаголовок = Неопределено Тогда
			КолонкаЗаголовок = Колонка.Имя;
		КонецЕсли;
		
		Элементы.ПолеСортировки.СписокВыбора.Добавить(Колонка.Имя, КолонкаЗаголовок);
	КонецЦикла;
	
КонецПроцедуры

// В процедуре ПриСозданииНаСервере или другом месте инициализации
&НаСервере
Процедура ИнициализацияТаблицыНаФорме()
	
	Если ТаблицаКанджи.Количество() = 0 Тогда
		HTMLДокумент = "<html><body><div style='padding:20px;'>Ещё не создано ни одного канджи</div></body></html>";
		Возврат;
	КонецЕсли;
	
	КлючНастройкиКлиентскогоПриложения = "Общее/НастройкиКлиентскогоПриложения";
	Настройка = ХранилищеСистемныхНастроек.Загрузить(КлючНастройкиКлиентскогоПриложения);
	СветлаяТема = Настройка.ТемаКлиентскогоПриложения = ТемаКлиентскогоПриложения.Светлая;
	
	// Используем ЗаписьXML для производительности
	ПостроительHTML = Новый ЗаписьXML;
	ПостроительHTML.УстановитьСтроку();
	
	// Шапка документа со стилями
	ПостроительHTML.ЗаписатьБезОбработки(ПолучитьШапкуHTML(СветлаяТема));
	
	// Основной контейнер для карточек
	ПостроительHTML.ЗаписатьБезОбработки("<div class='cards-container'>");
	
	// Формируем карточки
	Для Каждого Строка Из ТаблицаКанджи Цикл
		
		Если НЕ СтрокаПодходитПодПоиск(Строка) Тогда
			Продолжить;
		КонецЕсли;
		
		// Экранируем HTML-спецсимволы
		НаименованиеЭск = ЭкранироватьHTML(Строка.Наименование);
		ЗначениеЭск = ЭкранироватьHTML(Строка.ОсновноеЗначение);
		Если СтрДлина(ЗначениеЭск) > 100 Тогда
			ЗначениеЭск = Лев(ЗначениеЭск, 97) + "...";
		КонецЕсли;
		
		ЗначениеЭск = СтрЗаменить(ЗначениеЭск, Символы.ПС, "<br>");
		КодЭск = ЭкранироватьHTML(Строка.Код);
		
		HTMLЧтений = "<div class='card-readings'><table class='readings-table'>";
		Отбор = Новый Структура("Канджи", Строка.Ссылка);
		Чтения = ЧтенияКанджи.НайтиСтроки(Отбор);
		Счет = 0;
		Для Каждого Чтение Из Чтения Цикл
			
			ЧтениеНаименованиеЭск = ЭкранироватьHTML(Чтение.Наименование);
			ВидЧтения = ?(Чтение.ВидЧтения = Перечисления.ВидыЧтенийКанджи.Кунное, "кун.", "он.");
			HTMLЧтений = HTMLЧтений + 
			"<tr class='readings-row'>" +
			"<td class='readings-type'>" + ВидЧтения + "</td>" +
			"<td class='readings-name'>" + ЧтениеНаименованиеЭск + "</td>" +
			"</tr>";
			
			Счет = Счет + 1;
			Если Счет = 2 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		HTMLЧтений = HTMLЧтений + "</table></div>";
		
		HTMLКарточки = "
		|<div class='card' id ='" + Строка.Идентификатор + "'>
		|    <div class='card-code'>" + КодЭск + "</div>
		|    <div class='card-header'>
		|        <div class='card-symbol'>" + Строка.Представление + "</div>
		|        <div style='flex-grow:1; display:flex; flex-direction:column; overflow-y:auto;'>
		|            <div class='card-title'>" + НаименованиеЭск + "</div>
		|            " + HTMLЧтений + "
		|        </div>
		|    </div>
		|    <div class='card-value'>" + ЗначениеЭск + "</div>
		|</div>";
		
		ПостроительHTML.ЗаписатьБезОбработки(HTMLКарточки);
		
		
	КонецЦикла;
	
	ПостроительHTML.ЗаписатьБезОбработки("</div></body></html>");
	
	HTMLДокумент = ПостроительHTML.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКанджи(Ссылка)
	КлючОткрытия = Ссылка;
	ПараметрыФормы = Новый Структура("Ключ", КлючОткрытия);
	ОписаниеЗакрытия = Новый ОписаниеОповещения("ПриЗакрытииКанджи", ЭтотОбъект);
	ОткрытьФорму("Справочник.Канджи.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект, , , ,ОписаниеЗакрытия);
КонецПроцедуры

&НаСервере
Функция ПолучитьШапкуHTML(ЭтоСветлаяТема)
	
	Если ЭтоСветлаяТема Тогда
	    ЦветФона = "#f5f5f5";
	    ЦветФонаКарточки = "white";
	    ЦветТекстаЗаголовка = "#333";
	    ЦветЧтений = "#888888";
	    ЦветТекстаЗначения = "#444";
	    ЦветКода = "#999999";
	    ЦветГраницы = "#ddd";
	    ЦветТени = "rgba(0,0,0,0.15)";
	    ЦветФонаТаблички = "rgba(240, 240, 240, 0.7)";
	    ЦветГраницыТаблички = "rgba(200, 200, 200, 0.5)";
	Иначе
	    // Темная тема
	    ЦветФона = "#1e1e1e";
	    ЦветФонаКарточки = "#252526";
	    ЦветТекстаЗаголовка = "#ffffff";
	    ЦветЧтений = "#777777";
	    ЦветТекстаЗначения = "#999999";
	    ЦветКода = "#777777";
	    ЦветГраницы = "#3e3e42";
	    ЦветТени = "rgba(0,0,0,0.7)";
	    ЦветФонаТаблички = "rgba(60, 60, 60, 0.5)";
	    ЦветГраницыТаблички = "rgba(80, 80, 80, 0.5)";
	КонецЕсли;
	
	Шаблон = ШаблонШапкиHTMLДокумента();
	// Заменяем параметры
	Шаблон = СтрЗаменить(Шаблон, "&ЦветФона", ЦветФона);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветФонаКарточки", ЦветФонаКарточки);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветТекстаЗаголовка", ЦветТекстаЗаголовка);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветЧтений", ЦветЧтений);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветТекстаЗначения", ЦветТекстаЗначения);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветКода", ЦветКода);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветГраницы", ЦветГраницы);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветТени", ЦветТени);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветФонаТаблички", ЦветФонаТаблички);
	Шаблон = СтрЗаменить(Шаблон, "&ЦветГраницыТаблички", ЦветГраницыТаблички);
	
	Возврат Шаблон;
КонецФункции

&НаСервере
Процедура СортироватьКанджи()
	
	СортировкаПоУмолчанию = "Код";
	
	Сортировка = ПолеСортировки + ?(НаправлениеСортировки, " УБЫВ", "");
	
	Если ПолеСортировки = "" Тогда
		Сортировка = СортировкаПоУмолчанию;
	КонецЕсли;
	
	ТаблицаКанджи.Сортировать(Сортировка);

КонецПроцедуры

&НаСервере
Функция СтрокаПодходитПодПоиск(СтрокаКанджи)
	
	СтрокаПодходитПодПоиск = Истина;
	Если ЗначениеЗаполнено(ПолеПоиска) Тогда
		РП1 = СтрНайтиПоРегулярномуВыражению(СтрокаКанджи.ОсновноеЗначение, ПолеПоиска, , , , Истина);
		РП2 = СтрНайтиПоРегулярномуВыражению(СтрокаКанджи.Наименование, ПолеПоиска, , , , Истина);
		Если РП1.НачальнаяПозиция = 0 И РП2.НачальнаяПозиция = 0 Тогда
			СтрокаПодходитПодПоиск = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаПодходитПодПоиск И ЗначениеЗаполнено(ПолеПоискаПоЧтению) Тогда
		ОтборЧтение = Новый Структура("Канджи, Наименование", СтрокаКанджи.Ссылка, ПолеПоискаПоЧтению);
		Если ЗначениеЗаполнено(ПоискВидЧтения) Тогда
			ОтборЧтение.Вставить("ВидЧтения", ПоискВидЧтения);
		КонецЕсли;
		Чтения = ЧтенияКанджи.НайтиСтроки(ОтборЧтение);
		Если Чтения.Количество() = 0 Тогда
			СтрокаПодходитПодПоиск = Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат СтрокаПодходитПодПоиск;
	
КонецФункции

&НаСервере
Процедура ТаблицаКанджиОбновитьНаСервере()
	ЗаполнитьТаблицы();
	ИнициализацияТаблицыНаФорме();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьРасширенногоПоиска()
	Элементы.ГруппаПоискПоЧтению.Видимость = ВидимостьРасширенногоПоиска;
	Элементы.РасширенныйПоискПереключитьВидимость.Картинка = 
		?(ВидимостьРасширенногоПоиска, БиблиотекаКартинок.СкрытьПароль, БиблиотекаКартинок.ПоказатьДанные);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьМенюСортировки()
	
	Элементы.ГруппаСортировка1.Видимость = ВидимостьМенюСортировки;
	Элементы.СортировкаПереключитьВидимость.Картинка = 
		?(ВидимостьМенюСортировки, БиблиотекаКартинок.СкрытьПароль, БиблиотекаКартинок.СортироватьСписокПоВозрастанию);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШаблонШапкиHTMLДокумента()
	
	Возврат "
	|<!DOCTYPE html>
	|<html>
	|<head>
	|    <meta charset='UTF-8'>
	|    <meta http-equiv='X-UA-Compatible' content='IE=10'>
	|    <style>
	|        * {
	|            margin: 0;
	|            padding: 0;
	|            box-sizing: border-box;
	|        }
	|        
	|        body {
	|            font-family: Arial, sans-serif;
	|            background-color: &ЦветФона;
	|            padding: 10px;
	|        }
	|        
	|        .cards-container {
	|            display: grid;
	|            grid-template-columns: repeat(auto-fill, minmax(220px, 1fr));
	|            gap: 15px;
	|            width: 100%;
	|        }
	|        
	|        .card {
	|            background: &ЦветФонаКарточки;
	|            border: 1px solid &ЦветГраницы;
	|            border-radius: 8px;
	|            padding: 10px;
	|            height: 180px;
	|            display: flex;
	|            flex-direction: column;
	|            position: relative;
	|            overflow: hidden;
	|            cursor: pointer;
	|            transition: all 0.3s ease;
	|        }
	|        
	|        .card-code {
	|            position: absolute;
	|            top: 8px;
	|            right: 8px;
	|            font-size: 10px;
	|            color: &ЦветКода;
	|            opacity: 0.7;
	|            z-index: 1;
	|        }
	|        
	|        .card:hover {
	|            box-shadow: 0 4px 12px &ЦветТени;
	|            transform: translateY(-2px);
	|        }
	|        
	|        .card-header {
	|            display: flex;
	|            align-items: flex-start;
	|            flex-shrink: 0;
	|            flex-basis: 70%;
	|            min-height: 70%;
	|            max-height: 70%;
	|            overflow-y: auto;
	|        }
	|        
	|        .card-symbol {
	|            font-size: 70px;
	|            font-weight: bold;
	|            margin-right: 15px;
	|            line-height: 1;
	|            min-width: 50px;
	|            text-align: center;
	|            flex-shrink: 0;
	|        }
	|        
	|        .card-title {
	|            font-size: 20px;
	|            font-weight: bold;
	|            color: &ЦветТекстаЗаголовка;
	|            flex-grow: 1;
	|            word-break: break-word;
	|            margin-bottom: 4px;
	|        }
	|        
	|        .readings-table {
	|            border-collapse: collapse;
	|            width: 100%;
	|            background-color: &ЦветФонаТаблички;
	|            border-radius: 4px;
	|            overflow: hidden;
	|            border: 1px solid &ЦветГраницыТаблички;
	|        }
	|        
	|        .readings-row {
	|            border-bottom: 1px solid &ЦветГраницыТаблички;
	|        }
	|        
	|        .readings-row:last-child {
	|            border-bottom: none;
	|        }
	|        
	|        .readings-name {
	|            padding: 2px 4px 2px 2px;
	|            text-align: left;
	|            vertical-align: middle;
	|            font-size: 13px;
	|            white-space: nowrap;
	|            width: 75%;
	|            color: &ЦветЧтений;
	|        }
	|        
	|        .readings-type {
	|            padding: 2px 2px 2px 4px;
	|            text-align: center;
	|            vertical-align: middle;
	|            font-size: 11px;
	|            white-space: nowrap;
	|            width: 25%;
	|            font-weight: bold;
	|            color: &ЦветЧтений;
	|        }
	|        
	|        .card-value {
	|            flex-grow: 1;
	|            font-size: 15px;
	|            color: &ЦветТекстаЗначения;
	|            padding: 8px 0 4px 0;
	|            border-top: 1px solid &ЦветГраницы;
	|            word-break: break-word;
	|            overflow-y: auto;
	|            max-height: none;
	|            white-space: pre-line;
	|            display: -webkit-box;
	|             -webkit-line-clamp: 3;
	|             -webkit-box-orient: vertical;
	|            overflow: hidden;
	|            margin-top: auto;
	|        }
	|    </style>
	|	<script type = 'text/javascript'>
	|	
	| 	document.oncontextmenu = function(e) {
	|  		element = e.target;
	| 	 	document.getElementById('propertyName').innertext = 'КОНТЕКСТНОЕ_МЕНЮ';
	|  		element.click(); 
	|	}
	|	
	|</script>
	|</head>
	|<body>
	|<div id='propertyName' style='display: none'></div>";
КонецФункции

&НаСервере
Функция ЭкранироватьHTML(Текст)
	
	Если Текст = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, "&", "&amp;");
	Текст = СтрЗаменить(Текст, "<", "&lt;");
	Текст = СтрЗаменить(Текст, ">", "&gt;");
	Текст = СтрЗаменить(Текст, """", "&quot;");
	Текст = СтрЗаменить(Текст, "'", "&#39;");
	
	Возврат Текст;
	
КонецФункции

&НаКлиенте
Процедура ПриЗакрытииКанджи(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицы();
	ИнициализацияТаблицыНаФорме();
	
КонецПроцедуры

#КонецОбласти
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить обновить чтение.
// 
// Параметры:
//  Канджи - СправочникСсылка.Канджи - ссылка на канджи
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица слов:
//  * Слово - СправочникСсылка.Словарь - слово
Функция СловаПоКанджи(Канджи) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СловарьСостав.Ссылка КАК Слово
	|ИЗ
	|	Справочник.Словарь.Состав КАК СловарьСостав
	|ГДЕ
	|	СловарьСостав.Канджи = &Канджи
	|
	|УПОРЯДОЧИТЬ ПО
	|	СловарьСостав.Ссылка.Наименование";
	Запрос.УстановитьПараметр("Канджи", Канджи);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Текст запроса поиск дублей
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаПоискДублей() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	Канджи.Ссылка КАК Ссылка,
	|	Канджи.Наименование КАК Наименование,
	|	Канджи.Юникод КАК Юникод
	|ИЗ
	|	Справочник.Канджи КАК Канджи
	|ГДЕ
	|	Канджи.Юникод = &Юникод
	|	И Канджи.Ссылка <> &Ссылка
	|	И НЕ Канджи.ПометкаУдаления";
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	Поля.Добавить("Наименование");
	Поля.Добавить("ЮникодПредставление");
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Данные.Наименование) Тогда
		Представление = "Канджи (новый)";	
	Иначе
		Представление = СтрШаблон("%1 - %2", Данные.Наименование, Данные.ЮникодПредставление);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытийФормы

// Вызывается после создания формы, но перед ее отображением.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущиеНастройки = РегистрыСведений.НастройкиПодсказокКанджи.ТекущиеНастройки();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды "Сохранить".
//
&НаКлиенте
Процедура Сохранить(Команда)
	
	СтруктураНастроек = Новый Структура(
		"КоличествоРядовПодсказок, КоличествоСтолбцовПодсказок", КоличествоРядовПодсказок, КоличествоСтолбцовПодсказок);
	Если СохранитьНаСервере(СтруктураНастроек) Тогда
		Закрыть(СтруктураНастроек);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Сохраняет настройки в регистре сведений.
//
&НаСервереБезКонтекста
Функция СохранитьНаСервере(СтруктураНастроек)
	
	Попытка
		РегистрыСведений.НастройкиПодсказокКанджи.ЗаписатьНастройки(СтруктураНастроек);
		Возврат Истина;
	Исключение
		КраткийТекстОшибки = НСтр("ru = 'Не удалось записать настройки подсказок канджи'") + Символы.ПС + ОписаниеОшибки();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = КраткийТекстОшибки;
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

#КонецОбласти
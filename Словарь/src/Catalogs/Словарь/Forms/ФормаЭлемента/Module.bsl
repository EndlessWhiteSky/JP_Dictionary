#Область ОписаниеПеременных

&НаКлиенте
Перем Словарь;

&НаКлиенте
Перем ПредложенияКанджи;

&НаКлиенте
Перем ТекущийТекст;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСвязанныеСлова();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	ТекущийТекст = Объект.Наименование;
	СоставитьПредложенияКанджи();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьСвязанныеСлова();
	ЗаполнитьСвязанныеСлова();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Объект.Наименование = АзбукаКлиентСервер.КонвертироватьРаскладкуQWERTY(Объект.Наименование);
	Объект.Наименование = НРег(Объект.Наименование);
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана"));
	ТекущийТекст = Объект.Наименование;
	СоставитьПредложенияКанджи();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПредложениеКанджи(Команда)
	
	НомерКанджи = Число(СтрЗаменить(Команда.Имя, "ПредложениеКанджи", ""));
	Предложение = ПредложенияКанджи[НомерКанджи - 1];
	
	ЗаменитьКанджиНаСервере(Предложение);
	СоставитьПредложенияКанджи();
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСлог(Команда)
	
	ТекДанные = Элементы.Состав.ТекущиеДанные;
	Если ТекДанные = Неопределено ИЛИ ЗначениеЗаполнено(ТекДанные.Канджи) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана") Тогда
		ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Катакана");
		Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Смешанное");
	Иначе
		ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана");
	КонецЕсли;
	Модифицированность = Истина;
	СформироватьНаименованияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТокеныХирагана(Команда)
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана"));
	Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Японское");
	
КонецПроцедуры

&НаКлиенте
Процедура ТокеныКатакана(Команда)
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Катакана"));
	Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Заимствованное");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСоставНаСервере(Токены, ВидКаны)
	
	СлогиНаименования = Новый массив;
	Для Каждого Токен Из Токены Цикл
		Если Токен.ЭтоМусор Тогда
			Продолжить;
		КонецЕсли;
		СлогиНаименования.Добавить(Токен.Представление);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = АзбукаВызовСервера.ТекстЗапросаДанныеТокенов();
	Если ВидКаны = Перечисления.ВидыКаны.Катакана Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.Состав", "Слоги.СоставКатакана");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.ЗаписьХираганой", "Слоги.ЗаписьКатаканой");		
	КонецЕсли;
	Запрос.УстановитьПараметр("СлогиНаименования", СлогиНаименования);
	ДанныеСлогов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Объект.Состав.Очистить();
	Объект.ЗаписьНаЯпонском = "";
	
	Для Каждого Слог Из СлогиНаименования Цикл
		Отбор = Новый Структура("Наименование", Слог);
		ДанныеСлога = ДанныеСлогов.Строки.НайтиСтроки(Отбор)[0];
		НоваяСтрока = Объект.Состав.Добавить();
		НоваяСтрока.Слог = ДанныеСлога.Слог;
		НоваяСтрока.Запись = ВидКаны;
		Объект.ЗаписьНаЯпонском = Объект.ЗаписьНаЯпонском + ДанныеСлога.ЗаписьКаной;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьКанджиНаСервере(Предложение)
	
	ЗаменяемаяСтрока = Предложение.Наименование;
	ЗаменяемаяСтрока = СтрЗаменить(ЗаменяемаяСтрока, "_", "");
	
	КанджиЗамены = Предложение.ЮникодПредставление;
	Для Счет = 0 По Объект.Состав.Количество() - 1 Цикл
		Слог = Объект.Состав[Счет];
		Если ЗначениеЗаполнено(Слог.Канджи) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоНачалоЗамены(Счет, ЗаменяемаяСтрока) Тогда
			ИтогоСтрока = "";
			Пока Истина Цикл
				Слог = Объект.Состав[Счет];
				Слог.Канджи = Предложение.Канджи;
				Слог.Запись = Перечисления.ВидыКаны.Хирагана;
				Счет = Счет + 1;
				ДобавляемаяСтрока = Слог.Слог.Наименование;
				Если Счет > 0 И ПустаяСтрока(ИтогоСтрока) И ЭтоСлогСУдвоенной(ДобавляемаяСтрока) Тогда
					ДобавляемаяСтрока = Сред(ДобавляемаяСтрока, 2);
				КонецЕсли;
				ИтогоСтрока = ИтогоСтрока + ДобавляемаяСтрока;
				Если ИтогоСтрока = ЗаменяемаяСтрока Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьНаименованияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаименованияНаСервере()
	
	ЕстьКанджи = Ложь;
	Канджи = Неопределено;
	Объект.ЗаписьНаЯпонском = "";
	Для Каждого Слог Из Объект.Состав Цикл
		
		Если ЗначениеЗаполнено(Слог.Канджи) Тогда
			ЕстьКанджи = Истина;
			Если Слог.Канджи <> Канджи Тогда
				Канджи = Слог.Канджи;
				Объект.ЗаписьНаЯпонском = Объект.ЗаписьНаЯпонском + Канджи.ЮникодПредставление;
			КонецЕсли;
			
		Иначе
			Канджи = Неопределено;
			Если Слог.Запись = Перечисления.ВидыКаны.Катакана Тогда
				Объект.ЗаписьНаЯпонском = Объект.ЗаписьНаЯпонском + Слог.Слог.ЗаписьКатаканой;
			Иначе
				Объект.ЗаписьНаЯпонском = Объект.ЗаписьНаЯпонском + Слог.Слог.ЗаписьХираганой;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьКанджи Тогда
		Объект.Расшифровка = "";
		Для Каждого Слог Из Объект.Состав Цикл
			Если Слог.Запись = Перечисления.ВидыКаны.Катакана Тогда
				Объект.Расшифровка = Объект.Расшифровка + Слог.Слог.ЗаписьКатаканой;
			Иначе
				Объект.Расшифровка = Объект.Расшифровка + Слог.Слог.ЗаписьХираганой;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставитьПредложенияКанджи()
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(ТекущийТекст, Словарь);
	Для Счет = 0 По Токены.ВГраница() Цикл
		Если Счет >= Объект.Состав.Количество() Тогда
			Прервать;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Состав[Счет].Канджи) Тогда
			Токены[Счет].ЭтоМусор = Истина;
		КонецЕсли;
	КонецЦикла;
	ПредложенияКанджи = РаботаСоСтрокамиКлиентСервер.ПодобратьКанджи(Токены);
	УстановитьОформлениеПредложений(ПредложенияКанджи);
	
КонецПроцедуры

&НаСервере
Процедура СкрытьВидимостьПредложений()
	
	Счет = 0;
	Для Счет = 1 По 12 Цикл
		Элементы["ПредложениеКанджи" + Счет].Видимость = Ложь;
		ЭтаФорма.Команды.Найти("ПредложениеКанджи" + Счет).Подсказка = "";
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеПредложений(ПредложенияКанджи)
	
	СкрытьВидимостьПредложений();
	
	Если ПредложенияКанджи = Неопределено ИЛИ  ПредложенияКанджи.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Счет = 1 По ПредложенияКанджи.Количество() Цикл
		Элементы["ПредложениеКанджи" + Счет].Видимость = Истина;
		Элементы["ПредложениеКанджи" + Счет].Заголовок = ПредложенияКанджи[Счет - 1].ЮникодПредставление;
		ЭтаФорма.Команды.Найти("ПредложениеКанджи" + Счет).Подсказка = ПредложенияКанджи[Счет - 1].Наименование;
		Если Счет = 12 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЭтоНачалоЗамены(Знач ИндексСлога, ЗаменяемаяСтрока)
	
	СтрокаСравнения = "";
	Пока Истина Цикл
		
		Слог = Объект.Состав[ИндексСлога];
		ИндексСлога = ИндексСлога + 1;
		ДобавляемаяСтрока = Слог.Слог.Наименование;
		Если ИндексСлога > 0 И ПустаяСтрока(СтрокаСравнения) И ЭтоСлогСУдвоенной(ДобавляемаяСтрока) Тогда
			
			ДобавляемаяСтрока = Сред(ДобавляемаяСтрока, 2);
			
		КонецЕсли;
		СтрокаСравнения = СтрокаСравнения + ДобавляемаяСтрока;
		Если СтрокаСравнения = ЗаменяемаяСтрока Тогда
			Возврат Истина;
		ИначеЕсли СтрНачинаетсяС(ЗаменяемаяСтрока, СтрокаСравнения) Тогда
			Продолжить;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
КонецФункции

&НаСервере
Функция ЭтоСлогСУдвоенной(Слог)
	
	Слоги = Новый Соответствие;
	АзбукаПовтИсп.СоответствиеРКДобавитьСлогиСУдвоениемСогласных(Слоги);
	Возврат Слоги.Получить(Слог) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура СоставКанджиПриИзменении(Элемент)
	СформироватьНаименованияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязанныеСлова()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			НоваяСтрока = СвязанныеСлова.Добавить();
			НоваяСтрока.Слово = Параметры.ЗначениеКопирования;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	СвязанныеСлова.Очистить();
	Слова = РегистрыСведений.СвязанныеСлова.СвязанныеСлова(Объект.Ссылка);
	Для Каждого Слово Из Слова Цикл
		
		НоваяСтрока = СвязанныеСлова.Добавить();
		НоваяСтрока.Слово = Слово;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСвязанныеСлова()
	
	Набор1 = РегистрыСведений.СвязанныеСлова.СоздатьНаборЗаписей();
	Набор1.Отбор.Слово1.Установить(Объект.Ссылка);
	Набор1.Записать();
	
	Набор2 = РегистрыСведений.СвязанныеСлова.СоздатьНаборЗаписей();
	Набор2.Отбор.Слово2.Установить(Объект.Ссылка);
	Набор2.Записать();
	
	Для Каждого СтрокаСлово Из СвязанныеСлова Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаСлово.Слово) Тогда
			Продолжить;
		КонецЕсли;
		Запись = Набор1.Добавить();
		Запись.Слово1 = Объект.Ссылка;
		Запись.Слово2 = СтрокаСлово.Слово;
	КонецЦикла;
	Набор1.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

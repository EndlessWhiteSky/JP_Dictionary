#Область ОписаниеПеременных

&НаКлиенте
Перем Словарь;

&НаКлиенте
Перем ПредложенияКанджи;

&НаКлиенте
Перем ТекущийТекст;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущиеНастройки = РегистрыСведений.НастройкиПодсказокКанджи.ТекущиеНастройки();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеНастройки);
	
	РаботаСДублямиСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРаботыСДублями());
	
	СоздатьЭлементыПодсказок();
	ЗаполнитьСвязанныеСлова();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	ТекущийТекст = Объект.Наименование;
	РаботаСДублямиКлиент.ПоискДублей(ЭтотОбъект);
	СоставитьПредложенияКанджи();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьСвязанныеСлова();
	ЗаполнитьСвязанныеСлова();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	МассивСсылок = Новый Массив;
	Для Каждого Строка Из СвязанныеСлова Цикл
		МассивСсылок.Добавить(Строка.Слово);
	КонецЦикла;
	
	Оповестить("ЗаписьСлова", МассивСсылок);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьСлова" И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		ЗаполнитьСвязанныеСлова();
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаписьКанджи" Тогда
		СоставитьПредложенияКанджи();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Объект.Наименование = АзбукаКлиентСервер.КонвертироватьРаскладкуQWERTY(Объект.Наименование);
	Объект.Наименование = НРег(Объект.Наименование);
	Модифицированность = Истина;
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана"));
	ТекущийТекст = Объект.Наименование;
	РаботаСДублямиКлиент.ПоискДублей(ЭтотОбъект);
	СоставитьПредложенияКанджи();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставКанджиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	СформироватьНаименованияНаСервере();
	СоставитьПредложенияКанджи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ПредложениеКанджи(Команда)
	
	НомерКанджи = Число(СтрЗаменить(Команда.Имя, "ПредложениеКанджи", ""));
	Предложение = ПредложенияКанджи[НомерКанджи - 1];
	
	ЗаменитьКанджиНаСервере(Предложение);
	СоставитьПредложенияКанджи();
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСлог(Команда)
	
	ТекДанные = Элементы.Состав.ТекущиеДанные;
	Если ТекДанные = Неопределено ИЛИ ЗначениеЗаполнено(ТекДанные.Канджи) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана") Тогда
		ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Катакана");
		Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Смешанное");
	Иначе
		ТекДанные.Запись = ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана");
	КонецЕсли;
	Модифицированность = Истина;
	СформироватьНаименованияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТокеныХирагана(Команда)
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Хирагана"));
	Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Японское");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТокеныКатакана(Команда)
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	ЗаполнитьСоставНаСервере(Токены, ПредопределенноеЗначение("Перечисление.ВидыКаны.Катакана"));
	Объект.ВидСлова = ПредопределенноеЗначение("Перечисление.ВидыСлов.Заимствованное");
	Объект.Расшифровка = "";
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвязанныеСловаОбновить(Команда)
	ЗаполнитьСвязанныеСлова();
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	Оповещение = Новый ОписаниеОповещения("НастройкиЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Словарь.Форма.ФормаНастроек",
		,
		ЭтотОбъект, 
		УникальныйИдентификатор, 
		Окно,
		НавигационнаяСсылка,
		Оповещение,
		РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВладельца,
		РежимОтображенияОкнаФормы.ВОсновномОкне);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСоставНаСервере(Токены, ВидКаны)
	
	СлогиНаименования = СлогиНаименования(Токены);
	
	Запрос = Новый Запрос;
	Запрос.Текст = АзбукаВызовСервера.ТекстЗапросаДанныеТокенов();
	Если ВидКаны = Перечисления.ВидыКаны.Катакана Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.Состав", "Слоги.СоставКатакана");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.ЗаписьХираганой", "Слоги.ЗаписьКатаканой");		
	КонецЕсли;
	Запрос.УстановитьПараметр("СлогиНаименования", СлогиНаименования);
	ДанныеСлогов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Объект.Состав.Очистить();
	Объект.ЗаписьНаЯпонском = "";
	
	Для Каждого Слог Из СлогиНаименования Цикл
		
		Отбор = Новый Структура("Наименование", Слог);
		ДанныеСлога = ДанныеСлогов.Строки.НайтиСтроки(Отбор)[0];
		НоваяСтрока = Объект.Состав.Добавить();
		НоваяСтрока.Слог = ДанныеСлога.Слог;
		НоваяСтрока.Запись = ВидКаны;
		Объект.ЗаписьНаЯпонском = Объект.ЗаписьНаЯпонском + ДанныеСлога.ЗаписьКаной;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязанныеСлова()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			НоваяСтрока = СвязанныеСлова.Добавить();
			НоваяСтрока.Слово = Параметры.ЗначениеКопирования;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СвязанныеСлова.Очистить();
	Слова = РегистрыСведений.СвязанныеСлова.СвязанныеСлова(Объект.Ссылка);
	Для Каждого Слово Из Слова Цикл
		
		НоваяСтрока = СвязанныеСлова.Добавить();
		НоваяСтрока.Слово = Слово;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСвязанныеСлова()
	
	РеквизитФормыВЗначение("Объект").ЗаписатьСвязанныеСлова(СвязанныеСлова.Выгрузить().ВыгрузитьКолонку("Слово"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьКанджиНаСервере(Предложение)
	
	ЗаменяемаяСтрока = Предложение.Наименование;
	ЗаменяемаяСтрока = СтрЗаменить(ЗаменяемаяСтрока, "_", "");
	
	ПодробныеДанныеТЧСостав = ПодробныеДанныеТЧСостав();
	
	Для Счет = 0 По ПодробныеДанныеТЧСостав.Количество() - 1 Цикл
		
		Слог = ПодробныеДанныеТЧСостав[Счет];
		Если ЗначениеЗаполнено(Слог.Канджи) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЭтоНачалоЗамены(ПодробныеДанныеТЧСостав, Счет, ЗаменяемаяСтрока) Тогда
			Продолжить;
		КонецЕсли;
		
		ИтогоСтрока = "";
		Пока Истина Цикл
				
			Слог = ПодробныеДанныеТЧСостав[Счет];
			РедактируемыйСлог = Объект.Состав[Счет];
			РедактируемыйСлог.Канджи = Предложение.Канджи;
			РедактируемыйСлог.Запись = Перечисления.ВидыКаны.Хирагана;
				
			Счет = Счет + 1;
				
			ДобавляемаяСтрока = Слог.СлогНаименование;
			Если Счет > 0 И ПустаяСтрока(ИтогоСтрока) И ЭтоСлогСУдвоенной(ДобавляемаяСтрока) Тогда
				ДобавляемаяСтрока = Сред(ДобавляемаяСтрока, 2);
			КонецЕсли;
				
			ИтогоСтрока = ИтогоСтрока + ДобавляемаяСтрока;
			Если ИтогоСтрока = ЗаменяемаяСтрока Тогда
				Прервать;
			КонецЕсли;
				
		КонецЦикла;
		
		Прервать;
				
	КонецЦикла;
	Модифицированность = Истина;
	СформироватьНаименованияНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРаботыСДублями()
	
	ПараметрыРаботыСДублями = РаботаСДублямиСервер.Шаблон_ПараметрыРаботыСДублями();
	ПараметрыРаботыСДублями.ТекстЗапросаПоискДублей = Справочники.Словарь.ТекстЗапросаПоискДублей();
	ПараметрыЗапросаПоискДублей = Новый Структура;
	ПараметрыЗапросаПоискДублей.Вставить("Наименование", "Объект.Наименование");
	ПараметрыЗапросаПоискДублей.Вставить("Ссылка", "Объект.Ссылка");
	ПараметрыРаботыСДублями.ПараметрыЗапросаПоискДублей = ПараметрыЗапросаПоискДублей;
	Возврат ПараметрыРаботыСДублями;
	
КонецФункции

&НаСервере
Функция ПодробныеДанныеТЧСостав()
	Возврат РеквизитФормыВЗначение("Объект").ПодробныеДанныеТЧСостав();
КонецФункции

&НаКлиенте
Процедура Подключаемый_НадписьДублиНажатие()
	РаботаСДублямиКлиент.НадписьДублиНажатие(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
	
	СоздатьЭлементыПодсказок();
	СоставитьПредложенияКанджи();
	
КонецПроцедуры	

&НаСервере
Процедура СкрытьВидимостьПредложений()
	
	Счет = 0;
	Для Счет = 1 По КоличествоРядовПодсказок * КоличествоСтолбцовПодсказок Цикл
		Элементы["ПредложениеКанджи" + Счет].Видимость = Ложь;
		Команды.Найти("ПредложениеКанджи" + Счет).Подсказка = "";
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СлогиНаименования(ТокеныНаименования)
	
	СлогиНаименования = Новый Массив;
	
	Для Каждого Токен Из ТокеныНаименования Цикл	
		Если Токен.ЭтоМусор Тогда
			Продолжить;
		КонецЕсли;
		
		СлогиНаименования.Добавить(Токен.Представление);
		
	КонецЦикла;
	
	Возврат СлогиНаименования;
	
КонецФункции

&НаСервере
Процедура СоздатьЭлементыПодсказок()
	
	УдалитьЭлементыПодсказок();
	
	// Для каждого ряда подсказок создать группу "ГруппаПредложениеКанджи№группы" (1 по КоличествоРядовПодсказок)
	// В группу добавить кнопки "ПредложениеКанджи№группы№Кнопки", 
	// с пустым заголовком, ширина 5, команда и действие ПредложениеКанджи
	// Всего 1 по КоличествоСтолбцовПодсказок в каждой группе
	
	ПолныйНомерКнопки = 1;
	Для НомерГруппы = 1 По КоличествоРядовПодсказок Цикл
		
		ГруппаРяд = Элементы.Добавить("ГруппаПредложениеКанджи" + НомерГруппы, Тип("ГруппаФормы"), Элементы.ЛеваяКолонка);
		ГруппаРяд.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаРяд.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаРяд.ОтображатьЗаголовок = ЛОЖЬ; 
		ГруппаРяд.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		// Создаем команды и кнопки внутри группы
		Для НомерКнопки = 1 По КоличествоСтолбцовПодсказок Цикл
			
			ИмяКоманды = "ПредложениеКанджи" + ПолныйНомерКнопки;
			
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Заголовок = " ";
			НоваяКоманда.Действие = "Подключаемый_ПредложениеКанджи";
						
			НоваяКнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ГруппаРяд);
			НоваяКнопка.Заголовок = " ";
			НоваяКнопка.Ширина = 5;
			НоваяКнопка.ИмяКоманды = ИмяКоманды;
			
			ПолныйНомерКнопки = ПолныйНомерКнопки + 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура СоставитьПредложенияКанджи()
	
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(ТекущийТекст, Словарь);
	Для Счет = 0 По Токены.ВГраница() Цикл
		Если Счет >= Объект.Состав.Количество() Тогда
			Прервать;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Состав[Счет].Канджи) Тогда
			Токены[Счет].ЭтоМусор = Истина;
		КонецЕсли;
	КонецЦикла;
	ПредложенияКанджи = РаботаСоСтрокамиКлиентСервер.ПодобратьКанджи(Токены);
	УстановитьОформлениеПредложений(ПредложенияКанджи);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаименованияНаСервере()
	
	СловарьОбъект = РеквизитФормыВЗначение("Объект");
	СловарьОбъект.ЗаполнитьНаименованияПоСоставу();
	ЗначениеВРеквизитФормы(СловарьОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыПодсказок()
	
	// Удалить группы
	ЭлементыДляУдаления = Новый Массив();
	Для Каждого Элемент Из Элементы Цикл
		Если СтрНайти(Элемент.Имя, "ГруппаПредложениеКанджи") > 0
		И ТипЗнч(Элемент) = Тип("ГруппаФормы") 
		Тогда
			ЭлементыДляУдаления.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементУдаления Из ЭлементыДляУдаления Цикл
		Элементы.Удалить(ЭлементУдаления);
	КонецЦикла;
	
	// Удалить команды с именем включающим "ПредложениеКанджи"
	КомандыДляУдаления = Новый Массив();
	Для Каждого Команда Из Команды Цикл
		Если СтрНайти(Команда.Имя, "ПредложениеКанджи") > 0 Тогда
			КомандыДляУдаления.Добавить(Команда);
		КонецЕсли;
	КонецЦикла;
	Для Каждого КомандаДляУдаления Из КомандыДляУдаления Цикл
		Команды.Удалить(КомандаДляУдаления);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеПредложений(ПредложенияКанджи)
	
	СкрытьВидимостьПредложений();
	
	Если ПредложенияКанджи = Неопределено ИЛИ  ПредложенияКанджи.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Счет = 1 По ПредложенияКанджи.Количество() Цикл
		Элементы["ПредложениеКанджи" + Счет].Видимость = Истина;
		Элементы["ПредложениеКанджи" + Счет].Заголовок = ПредложенияКанджи[Счет - 1].ЮникодПредставление;
		Команды.Найти("ПредложениеКанджи" + Счет).Подсказка = ПредложенияКанджи[Счет - 1].Наименование;
		Если Счет = КоличествоРядовПодсказок * КоличествоСтолбцовПодсказок Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЭтоНачалоЗамены(ТЧСостав, Знач ИндексСлога, ЗаменяемаяСтрока)
	
	СтрокаСравнения = "";
	Пока Истина Цикл
		
		Слог = ТЧСостав[ИндексСлога];
		ИндексСлога = ИндексСлога + 1;
		
		ДобавляемаяСтрока = Слог.СлогНаименование;
		Если ИндексСлога > 0 И ПустаяСтрока(СтрокаСравнения) И ЭтоСлогСУдвоенной(ДобавляемаяСтрока) Тогда
			ДобавляемаяСтрока = Сред(ДобавляемаяСтрока, 2);
		КонецЕсли;
		
		СтрокаСравнения = СтрокаСравнения + ДобавляемаяСтрока;
		
		Если СтрокаСравнения = ЗаменяемаяСтрока Тогда
			Возврат Истина;
		ИначеЕсли СтрНачинаетсяС(ЗаменяемаяСтрока, СтрокаСравнения) Тогда
			Продолжить;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоСлогСУдвоенной(Слог)
	
	Слоги = Новый Соответствие;
	АзбукаПовтИсп.СоответствиеРКДобавитьСлогиСУдвоениемСогласных(Слоги);
	Возврат Слоги.Получить(Слог) <> Неопределено;
	
КонецФункции

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Состав.Количество() > 0 Тогда
		ПервыйСлог = Состав[0].Слог;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Канджи") Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьПоКанджи(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДополнительныеСвойства.Вставить("ИсточникКопирования", ОбъектКопирования.Ссылка);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСвязанныеСлова();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ И ПометкаУдаления Тогда
		УдалитьСвязанныеСлова();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура УдалитьСвязанныеСлова() Экспорт

	Набор1 = РегистрыСведений.СвязанныеСлова.СоздатьНаборЗаписей();
	Набор1.Отбор.Слово1.Установить(Ссылка);
	Набор1.Записать();
	
	Набор2 = РегистрыСведений.СвязанныеСлова.СоздатьНаборЗаписей();
	Набор2.Отбор.Слово2.Установить(Ссылка);
	Набор2.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоКанджи(Канджи)
	КанджиОбъект = Канджи.ПолучитьОбъект();
	Наименование = КанджиОбъект.Наименование;
	ЗаписьНаЯпонском = КанджиОбъект.ЮникодПредставление;

	Комментарий = КанджиОбъект.ОсновноеЗначение;

	Чтение = КанджиОбъект.Чтения[0].Чтение;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЧтенияКанджиСостав.Слог КАК Слог,
	|	ЧтенияКанджиСостав.Слог.ЗаписьХираганой КАК СлогЗаписьХираганой
	|ИЗ
	|	Справочник.ЧтенияКанджи.Состав КАК ЧтенияКанджиСостав
	|ГДЕ
	|	ЧтенияКанджиСостав.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Чтение);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Состав.Добавить();
		НоваяСтрока.Слог = Выборка.Слог;
		НоваяСтрока.Запись = Перечисления.ВидыКаны.Хирагана;
		НоваяСтрока.Канджи = Канджи;
		Расшифровка = Расшифровка + Выборка.СлогЗаписьХираганой;
	КонецЦикла;
	ПервыйСлог = Состав[0].Слог;
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

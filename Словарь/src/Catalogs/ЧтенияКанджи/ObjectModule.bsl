#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обработчик события ПриИзмененииНаименования.
//
// Параметры:
//  Токены - Массив из Структура - Токены наименования.
//
Процедура ПриИзмененииНаименования(Токены) Экспорт
	
	СлогиНаименования = Новый массив;
	Для Каждого Токен Из Токены Цикл
		Если Токен.ЭтоМусор Тогда
			Продолжить;
		КонецЕсли;
		СлогиНаименования.Добавить(Токен.Представление);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = АзбукаВызовСервера.ТекстЗапросаДанныеТокенов();
	Если ВидЧтения = Перечисления.ВидыЧтенийКанджи.Онное Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.Состав", "Слоги.СоставКатакана");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Слоги.ЗаписьХираганой", "Слоги.ЗаписьКатаканой");		
	КонецЕсли;
	Запрос.УстановитьПараметр("СлогиНаименования", СлогиНаименования);
	ДанныеСлогов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Состав.Очистить();
	НаименованиеНаРусском = "";
	ЗаписьКаной = "";
	
	Для Каждого Слог Из СлогиНаименования Цикл
		Отбор = Новый Структура("Наименование", Слог);
		ДанныеСлога = ДанныеСлогов.Строки.НайтиСтроки(Отбор)[0];
		НоваяСтрока = Состав.Добавить();
		НоваяСтрока.Слог = ДанныеСлога.Слог;
		Если ДанныеСлога.НаименованиеНаРусском = "сокуон" Тогда
			ДанныеСлога.НаименованиеНаРусском = "_";
		КонецЕсли;
		НаименованиеНаРусском = НаименованиеНаРусском + ДанныеСлога.НаименованиеНаРусском;
		ЗаписьКаной = ЗаписьКаной + ДанныеСлога.ЗаписьКаной;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
	Если ДанныеЗаполнения = Неопределено И НЕ ПустаяСтрока(ТекстЗаполнения) Тогда
		Наименование = НРег(ТекстЗаполнения);
		Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Наименование, Словарь);
		ПриИзмененииНаименования(Токены);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ВидЧтения = ДанныеЗаполнения.ВидЧтения;
		Наименование = НРег(ДанныеЗаполнения.Наименование);
		Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Наименование, Словарь);
		ПриИзмененииНаименования(Токены);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Иначе 
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
#Область ОписаниеПеременных

// Словарь, в котором хранятся соответствия ромаджи и данных слогов
&НаКлиенте
Перем Словарь;
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Словарь = АзбукаВызовСервера.СоответствиеРомаджиДаннымСлогов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Объект.Наименование = НРег(Объект.Наименование);
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	НаименованиеПриИзмененииНаСервере(Токены);
КонецПроцедуры

&НаКлиенте
Процедура ВидЧтенияПриИзменении(Элемент)
	Токены = РаботаСоСтрокамиКлиентСервер.Токенизация(Объект.Наименование, Словарь);
	НаименованиеПриИзмененииНаСервере(Токены);
	УстановитьУсловнуюВидимость();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НаименованиеПриИзмененииНаСервере(Токены)
	
	СпрОбъект = РеквизитФормыВЗначение("Объект");
	СпрОбъект.ПриИзмененииНаименования(Токены);
	ЗначениеВРеквизитФормы(СпрОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьУсловнуюВидимость()
	
	ОнноеЧтение = Объект.ВидЧтения = ПредопределенноеЗначение("Перечисление.ВидыЧтенийКанджи.Онное");
	Элементы.СоставСлогЗаписьКатаканой.Видимость = ОнноеЧтение;
	Элементы.СоставСлогЗаписьХираганой.Видимость = НЕ ОнноеЧтение;
	
КонецПроцедуры

#КонецОбласти
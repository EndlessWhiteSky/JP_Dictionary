#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить обновить чтение.
// 
// Параметры:
//  ЧтениеСтрока - Строка - запись чтения на латинице
//  ВидЧтения - ПеречислениеСсылка.ВидыЧтенийКанджи - Вид чтения
// 
// Возвращаемое значение:
//  СправочникСсылка.ЧтенияКанджи - Чтение созданное или прочитанное из базы
Функция ПолучитьОбновитьЧтение(ЧтениеСтрока, ВидЧтения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧтенияКанджи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЧтенияКанджи КАК ЧтенияКанджи
	|ГДЕ
	|	ЧтенияКанджи.ПометкаУдаления = ЛОЖЬ
	|	И ЧтенияКанджи.Наименование = &ЧтениеСтрока
	|	И ЧтенияКанджи.ВидЧтения = &ВидЧтения";
	Запрос.УстановитьПараметр("ЧтениеСтрока", ЧтениеСтрока);
	Запрос.УстановитьПараметр("ВидЧтения", ВидЧтения);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		НовоеЧтение = СоздатьЭлемент();
		ДанныеЗаполнения = Новый Структура("Наименование, ВидЧтения", ЧтениеСтрока, ВидЧтения);
		НовоеЧтение.Заполнить(ДанныеЗаполнения);
		НовоеЧтение.Записать();
		Возврат НовоеЧтение.Ссылка;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли